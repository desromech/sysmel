let codeX86_64Block := {:builder |
    let addFunctionSymbol := (builder symbolNamed: #add)
        makeFunction;
        makePublic;
        yourself.

    builder
        textSection;
        align: 16sz unreachable: true;
        blockNamed: addFunctionSymbol with: {:builder |
            builder
                x86::endbr64;
                x86::mov: X86::RAX src: X86::RDI;
                x86::add: X86::RAX with: X86::RSI;
                x86::ret
        };
        align: 16sz unreachable: true;
        blockNamed: ((builder symbolNamed: #main) makeFunction; makePublic; yourself) with: {:builder |
            builder
                x86::endbr64;
                x86::push: X86::RBP;
                x86::mov: X86::RBP src: X86::RSP;

                x86::mov: X86::RDI src: 2i32 asAsmImmediate;
                x86::mov: X86::RSI src: 3i32 asAsmImmediate;
                x86::call: addFunctionSymbol dword;

                x86::mov: X86::RSP src: X86::RBP;
                x86::pop: X86::RBP;
                x86::ret
        }.
}.

let codeX86_64Elf := AsmStream for: AsmCompilationTarget x86_64 useElf buildWith: codeX86_64Block.
let codeX86_64Coff := AsmStream for: AsmCompilationTarget x86_64 useCoff buildWith: codeX86_64Block.

printLine(codeX86_64Elf fullPrintString).
printLine(codeX86_64Elf compilationTarget).
codeX86_64Elf assemble writeObjectToFileNamed: "test.x86_64.elf64".
codeX86_64Coff assemble writeObjectToFileNamed: "test.x86_64.coff".

let codeI386Block := {:builder |
    let addFunctionSymbol := (builder symbolNamed: #add)
        makeFunction;
        makePublic;
        yourself.

    builder
        textSection;
        align: 16sz unreachable: true;
        blockNamed: addFunctionSymbol with: {:builder |
            builder
                x86::endbr32;
                x86::mov: X86::EAX src: X86::ESP x86::dword + 8i8 asAsmImmediate;
                x86::add: X86::EAX with: X86::ESP x86::dword + 4i8 asAsmImmediate;
                x86::ret
        };
        align: 16sz unreachable: true;
        blockNamed: ((builder symbolNamed: #main) makeFunction; makePublic; yourself) with: {:builder |
            builder
                x86::endbr32;
                x86::push: X86::EBP;
                x86::mov: X86::EBP src: X86::ESP;

                x86::push: 2i32 asAsmImmediate;
                x86::push: 3i32 asAsmImmediate;
                x86::call: addFunctionSymbol dword;
                x86::add: X86::ESP with: 8i8 asAsmImmediate;

                x86::mov: X86::ESP src: X86::EBP;
                x86::pop: X86::EBP;
                x86::ret
        }.
}.
let codeI386Elf := AsmStream for: AsmCompilationTarget x86 useElf buildWith: codeI386Block.
let codeI386Coff := AsmStream for: AsmCompilationTarget x86 useCoff buildWith: codeI386Block.

printLine(codeI386Elf fullPrintString).
##printLine(codeI386Elf compilationTarget).
codeI386Elf assemble writeObjectToFileNamed: "test.i386.elf32".
codeI386Coff assemble writeObjectToFileNamed: "test.i386.coff".

let codeArmBlock := {:builder |
    builder
        textSection;
        align: 16sz unreachable: true;
        blockNamed: ((builder symbolNamed: #main) makeFunction; makePublic; yourself) with: {:builder |
            builder
                arm::bkpt: 45u16;
                arm::swi: 42u32
        }.
}.
let codeArmElf := AsmStream for: AsmCompilationTarget armv6k useElf buildWith: codeArmBlock.
let codeArmCoff := AsmStream for: AsmCompilationTarget armv6k useCoff buildWith: codeArmBlock.
printLine(codeArmElf fullPrintString).
codeArmElf assemble writeObjectToFileNamed: "test.arm.elf32".
codeArmCoff assemble writeObjectToFileNamed: "test.arm.coff".
