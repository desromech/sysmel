public class CLISysmelInterpreter definition: {
    public field programName => String.
    public field scanOnly => Boolean.
    public field parseOnly => Boolean.
    public field printStats => Boolean.
    public field evaluationTimeToRun => Int64.

    public method main: (arguments: Array) ::=> Int32 := {
        programName := arguments first.

        let i mutable := 1sz.
        while: (i < arguments size) do: {
            let argument => String := arguments at: i.
            (argument beginsWith: "-") ifTrue: {
                argument = "-version" ifTrue: {
                    self printVersion.
                    continue()
                }.
                argument = "-help" ifTrue: {
                    continue()
                }.
                argument = "-e" ifTrue: {
                    i := i + 1sz.
                    self evaluateString: (arguments at: i).
                    continue()
                }.
                argument = "-scan-only" ifTrue: {
                    scanOnly := true.
                    continue()
                }.
                argument = "-parse-only" ifTrue: {
                    scanOnly := true.
                    continue()
                }.
                argument = "-print-stats" ifTrue: {
                    printStats := true.
                    continue()
                }.
                argument = "-p" ifTrue: {
                    i := i + 1sz.
                    printLine(self evaluateString: (arguments at: i)).
                    continue()
                }.
                argument = "-save-obj-file" ifTrue: {
                    i := i + 1sz.
                    self saveNativeImageObjectFile: (arguments at: i).
                    continue()
                }.
            } ifFalse: {
                self evaluateFileNamed: argument
            }
        } continueWith: (i := i + 1sz).

        printStats ifTrue: {
            printLine("Evaluation time to run: ", evaluationTimeToRun asFloat32 * 0.001f32)
        }.

        0i32
    }.

    public method saveNativeImageObjectFile: (fileName: String) ::=> Void := {
        CLISysmelInterpreter buildNativeInterpreterImage saveTo: fileName
    }.

    public method evaluateFileNamed: (fileName: String) ::=> Void := {
        let sourceCodeText := IO::readWholeFileNamedAsString(fileName).
        let directory := FileSystem::dirname(fileName).
        let basename := FileSystem::basename(fileName).

        let sourceCode := SourceCode text: sourceCodeText directory: directory name: basename language: #sysmel.
        self evaluteSourceCode: sourceCode
    }.

    public method evaluateString: (sourceString: String) ::=> AnyValue := {
        let sourceCode := SourceCode text: sourceString directory: "" name: "<cli>" language: #sysmel.
        self evaluteSourceCode: sourceCode
    }.

    public method evaluteSourceCode: (sourceCode: SourceCode) ::=> AnyValue := {
        let result mutable => AnyValue := nil.
        evaluationTimeToRun := evaluationTimeToRun + { :: Void |
            scanOnly ifTrue: {
                result := (SysmelLanguage scanSourceCode: sourceCode).
                return: void
            }.

            parseOnly ifTrue: {
                result := (SysmelLanguage parseSourceCode: sourceCode).
                return: void
            }.
            result := SysmelLanguage analyzeAndEvaluateSourceCode: sourceCode.
        } timeToRunInMicroseconds.
        result
    }.

    public method printHelp => Void := {
        printLine(programName, " <files>...").
    }.

    public method printVersion => Void := {
        printLine(programName, " version 0.1").
    }.
}.

public function CLISysmelInterpreter::nativeMain(argc: Int32, argv: Char8 const pointer pointer) => Int32 := {
    let arguments := Array new: argc asSize.
    let i mutable := 0i32.
    while: (i < argc) do: {
        arguments at: i asSize put: (String fromCString: argv[i asIntPointer]).
    } continueWith: (i := i + 1i32).

    CLISysmelInterpreter new main: arguments.
}.

public function CLISysmelInterpreter::nativeRuntimeMain(argc: Int32, argv: Char8 const pointer pointer) => Int32 := {
    GarbageCollector::initialize().

    ## FIXME: Remove this line after generating global variables with initial values.
    FunctionBytecodeCompiler::Current := FunctionBytecodeDirectCompiler.

    let result := CLISysmelInterpreter::nativeMain(argc, argv).
    AnalysisQueue current waitPendingAnalysis.
    GarbageCollector::shutdown().
    result
}.
