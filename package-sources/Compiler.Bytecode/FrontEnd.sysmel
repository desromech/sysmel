
FunctionBytecodeDirectCompiler definition: {
    public method compileASTNode: (node: ASTNode) ::=> FunctionBytecodeAssemblerVectorOperand
        := self assembler withASTNode: node do: {| node doCompileIntoBytecodeWith: self }.
}.

ASTNode definition: {
    public final eager method compileBytecodesDirectlyWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand
        := compiler compileASTNode: self.

    public abstract eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand
        := self subclassResponsibility
}.

ASTBreakNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand
        := compiler assembler
            jumpTo: compiler breakLabel;
            literal: nil
}.

ASTCoerceValueNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let type := self typeExpression compileBytecodesDirectlyWith: compiler.
        let value := self valueExpression compileBytecodesDirectlyWith: compiler.
        let result := compiler assembler temporary: #coercedValue type: self analyzedType.
        compiler assembler coerceValue: value toType: type in: result.
        result
    }.      
}.

ASTContinueNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand
        := compiler assembler
            jumpTo: compiler continueLabel;
            literal: nil
}.

ASTDoWhileContinueWithNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let assembler := compiler assembler.

        let doWhileEntryLabel := assembler beginLabelNamed: #doWhileEntry.
        let doWhileCondition := assembler label: #doWhileCondition.
        let doWhileContinue := assembler label: #doWhileContinue.
        let doWhileMergeLabel := assembler label: #doWhileMerge.

        ## Body
        self bodyExpression ifNotNil: {:(ASTNode)expression :: Void |
            compiler withBreakLabel: doWhileMergeLabel continueLabel: doWhileContinue do: { :: Void |
                expression compileBytecodesDirectlyWith: compiler
            }.
        }.
        assembler jumpTo: doWhileCondition.

        ## Condition.
        assembler beginLabel: doWhileCondition.
        self conditionExpression ifNotNil: {:(ASTNode)expression :: Void |
            let condition := expression compileBytecodesDirectlyWith: compiler.
            assembler jumpTo: doWhileMergeLabel ifFalse: condition.
        } ifNil: {
            assembler jumpTo: doWhileContinue.
        }.
        
        ## Continue
        assembler beginLabel: doWhileContinue.
        self continueExpression ifNotNil: {:(ASTNode)expression :: Void |
            expression compileBytecodesDirectlyWith: compiler
        }.
        assembler jumpTo: doWhileEntryLabel.

        ## End loop and merge.
        assembler
            beginLabel: doWhileMergeLabel;
            literal: void
    }
}.

ASTDownCastNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let type := self typeExpression compileBytecodesDirectlyWith: compiler.
        let value := self valueExpression compileBytecodesDirectlyWith: compiler.
        let result := compiler assembler temporary: #downcastedValue type: self analyzedType.
        compiler assembler downCastValue: value toType: type in: result.
        result
    }.
}.

ASTLambdaNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let functionDefinition := self functionDefinition.
        functionDefinition ensureAnalysis.

        let functionDefinitionValue := compiler functionDefinitionValueFor: functionDefinition.

        let captures := functionDefinition sourceAnalyzedDefinition captures collect: {:(SymbolCaptureBinding)capture :: FunctionBytecodeAssemblerVectorOperand |
            compiler getValueForLocalBinding: capture sourceBinding
        }.

        let result := compiler assembler temporary: #lambda type: self analyzedType.
        compiler assembler makeClosure: functionDefinitionValue withCaptures: captures in: result.
        result
    }.
}.

ASTLexicalBlockNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand
        := compiler assembler withSourceEnvironment: self bodyEnvironment do: {:: FunctionBytecodeAssemblerVectorOperand | self body compileBytecodesDirectlyWith: compiler }.
}.

ASTLiteralNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand
        := compiler assembler literal: self value.
}.

ASTVariableDefinitionNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let value := self valueExpression compileBytecodesDirectlyWith: compiler.

        let assembler := compiler assembler.

        self isMutable ifTrue: {
            let localVariable := assembler temporary: #localVariable type: self analyzedType.
            assembler allocaWithValue: value inPointerOfType: (assembler literal: self analyzedType) in: localVariable.
            compiler setValue: localVariable forBinding: self binding.
            localVariable
        } ifFalse: {
            compiler setValue: value forBinding: self binding.
            value
        }
    }.
}.

SymbolBinding extend: {
    public abstract method compileBytecodesDirectlyWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand
        := self subclassResponsibility.
}.

SymbolAnalysisBinding extend: {
    public override method compileBytecodesDirectlyWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand
        := compiler getValueForLocalBinding: self.
}.

SymbolTupleSlotBinding extend: {
    public override method compileBytecodesDirectlyWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let assembler := compiler assembler.
        let tupleValue := compiler getValueForLocalBinding: tupleBinding.
        let reference := assembler temporary: #slotRef type: type.
        
        tupleBinding type isPointerLikeType ifTrue: {
            assembler ref: tupleValue slotReferenceAt: (assembler literal: typeSlot) into: reference
        } ifFalse: {
            assembler tuple: tupleValue slotReferenceAt: (assembler literal: typeSlot) into: reference
        }.

        reference
    }.
}.

SymbolValueBinding extend: {
    public override method compileBytecodesDirectlyWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let assembler := compiler assembler.
        let result := assembler temporary: self name type: self type.
        assembler loadSymbolValueBinding: (assembler literal: self) into: result.
        result
    }.
}.

ASTIdentifierReferenceNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand
        := self binding compileBytecodesDirectlyWith: compiler.
}.

ASTIfNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let assembler := compiler assembler.

        let condition := self conditionExpression compileBytecodesDirectlyWith: compiler.

        let result := assembler temporary: #ifResult type: self analyzedType.
        let ifFalseBranch := assembler label: #ifFalseBranch.
        let ifMerge := assembler label: #ifMerge.

        ## IfTrue branch.
        assembler
            jumpTo: ifFalseBranch ifFalse: condition.

        let trueResult := self trueExpression
            ifNotNil: {:(ASTNode)expression :: FunctionBytecodeAssemblerVectorOperand | expression compileBytecodesDirectlyWith: compiler }
            ifNil: (assembler literal: void).

        assembler
            move: trueResult into: result;
            jumpTo: ifMerge.

        ## IfFalse branch.
        assembler beginLabel: ifFalseBranch.
        let falseResult := self falseExpression
            ifNotNil: {:(ASTNode)expression :: FunctionBytecodeAssemblerVectorOperand | expression compileBytecodesDirectlyWith: compiler }
            ifNil: (assembler literal: void).

        assembler
            move: falseResult into: result;
            jumpTo: ifMerge.

        ## Merge
        assembler beginLabel: ifMerge.
        result
    }.
}.

ASTSwitchNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let assembler := compiler assembler.

        let value := self expression compileBytecodesDirectlyWith: compiler.

        let caseKeys := self caseExpressions collect: {:(ASTCaseNode)eachCase :: FunctionBytecodeAssemblerVectorOperand |
            eachCase keyExpression compileBytecodesDirectlyWith: compiler
        }.
        let caseLabels := self caseExpressions collect: {:(ASTCaseNode)eachCase :: FunctionBytecodeAssemblerLabel |
            assembler label: #caseLabel
        }.

        let result := assembler temporary: #switchResult type: self analyzedType.
        let defaultCase := assembler label: #switchDefaultCase.
        let switchMerge := assembler label: #switchMerge.

        assembler caseJumpOn: value keys: caseKeys destinations: caseLabels defaultDestination: defaultCase.

        self caseExpressions doWithIndex: {:(ASTCaseNode)eachCase :(Size)caseIndex :: Void |
            let key => FunctionBytecodeAssemblerVectorOperand := caseKeys at: caseIndex.
            let destination => FunctionBytecodeAssemblerLabel := caseLabels at: caseIndex.

            assembler beginLabel: destination.
            let caseValue := eachCase valueExpression compileBytecodesDirectlyWith: compiler.
            assembler
                move: caseValue into: result;
                jumpTo: switchMerge.
        }.

        assembler
            beginLabel: defaultCase;
            move: (self defaultExpression
                ifNotNil: (self defaultExpression compileBytecodesDirectlyWith: compiler)
                ifNil: (assembler literal: nil)) into: result;
            jumpTo: switchMerge;
            beginLabel: switchMerge.
        result
    }.
}.

ASTWhileContinueWithNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let assembler := compiler assembler.

        let entryLabel := assembler beginLabelNamed: #whileEntry.
        let bodyLabel := assembler label: #whileBody.
        let mergeLabel := assembler label: #whileMerge.
        let continueLabel := assembler label: #whileContinue.

        ## Condition.
        self conditionExpression ifNotNil: {:(ASTNode)expression :: Void |
            let condition := expression compileBytecodesDirectlyWith: compiler.
            assembler jumpTo: mergeLabel ifFalse: condition.
        } ifNil: {
            assembler jumpTo: bodyLabel.
        }.
        
        ## Body
        assembler beginLabel: bodyLabel.
        self bodyExpression ifNotNil: {:(ASTNode)expression :: Void |
            compiler withBreakLabel: mergeLabel continueLabel: continueLabel do: { :: Void |
                expression compileBytecodesDirectlyWith: compiler
            }.
        }.
        
        ## Continue
        assembler beginLabel: continueLabel.
        self continueExpression ifNotNil: {:(ASTNode)expression :: Void |
            expression compileBytecodesDirectlyWith: compiler
        }.

        ## End loop and merge.
        assembler
            jumpTo: entryLabel;
            beginLabel: mergeLabel;
            literal: void
    }
}.

let primitiveFunctionGenerators := #{
    #PointerLikeType::load : {:(ASTFunctionApplicationNode)self :(FunctionBytecodeDirectCompiler)compiler :: FunctionBytecodeAssemblerVectorOperand |
        let pointer := self arguments first compileBytecodesDirectlyWith: compiler.
        let result := compiler assembler temporary: #load type: self analyzedType.
        compiler assembler loadPointer: pointer in: result.
        result
    }.
    #PointerLikeType::store: : {:(ASTFunctionApplicationNode)self :(FunctionBytecodeDirectCompiler)compiler :: FunctionBytecodeAssemblerVectorOperand |
        let pointer := self arguments first compileBytecodesDirectlyWith: compiler.
        let value := self arguments second compileBytecodesDirectlyWith: compiler.
        compiler assembler storeValue: value inPointer: pointer.
        pointer
    }.
    #Void::fromAnyValue : {:(ASTFunctionApplicationNode)self :(FunctionBytecodeDirectCompiler)compiler :: FunctionBytecodeAssemblerVectorOperand |
        self arguments do: {:(ASTNode)each :: Void | each compileBytecodesDirectlyWith: compiler }.
        compiler assembler literal: void
    }.
    #yourself : {:(ASTFunctionApplicationNode)self :(FunctionBytecodeDirectCompiler)compiler :: FunctionBytecodeAssemblerVectorOperand |
        self arguments first compileBytecodesDirectlyWith: compiler
    }.
}.

ASTFunctionApplicationNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let functionExpression := self functionExpression.
        let primitiveGenerator := primitiveFunctionGenerators atOrNil: functionExpression literalFunctionPrimitiveName.

        primitiveGenerator ifNotNil: {
            primitiveGenerator(self, compiler)
        } ifNil: {
            let function := (functionExpression compileBytecodesDirectlyWith: compiler).
            let arguments := (self arguments collect: {:(ASTNode)each :: FunctionBytecodeAssemblerVectorOperand |
                each compileBytecodesDirectlyWith: compiler
            }).

            let result := compiler assembler temporary: #sendResult type: self analyzedType.
            self isNoTypechecked ifNotNil: {
                compiler assembler uncheckedCall: function arguments: arguments result: result
            } ifNil: {
                compiler assembler call: function arguments: arguments result: result
            }.

            result
        }.
    }.
}.

ASTMakeAssociationNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let key := self key compileBytecodesDirectlyWith: compiler.
        let value := self value
            ifNotNil: {:(ASTNode)value | value compileBytecodesDirectlyWith: compiler}
            ifNil: { compiler assembler literal: nil }.

        let result := compiler assembler temporary: #makeAssociation type: self analyzedType.
        compiler assembler makeAssociationWithKey: key value: value in: result.
        result
    }.
}.

ASTMakeByteArrayNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let elements := self elements collect: {:(ASTNode)each :: FunctionBytecodeAssemblerVectorOperand | each compileBytecodesDirectlyWith: compiler }.
        let result := compiler assembler temporary: #makeByteArray type: self analyzedType.
        compiler assembler makeByteArrayWithElements: elements in: result.
        result
    }.
}.

ASTMakeArrayNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let elements := self elements collect: {:(ASTNode)each :: FunctionBytecodeAssemblerVectorOperand | each compileBytecodesDirectlyWith: compiler }.
        let result := compiler assembler temporary: #makeArray type: self analyzedType.
        compiler assembler makeArrayWithElements: elements in: result.
        result
    }.
}.

ASTMakeDictionaryNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let elements := self elements collect: {:(ASTNode)each :: FunctionBytecodeAssemblerVectorOperand | each compileBytecodesDirectlyWith: compiler }.
        let result := compiler assembler temporary: #makeDictionary type: self analyzedType.
        compiler assembler makeDictionaryWithElements: elements in: result.
        result
    }.
}.

ASTMessageSendNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        self receiverLookupType ifNotNil: {:(ASTNode)receiverLookupTypeNode |
            let receiver := self receiver compileBytecodesDirectlyWith: compiler.
            let receiverLookupType := receiverLookupTypeNode compileBytecodesDirectlyWith: compiler.
            let selector := self selector compileBytecodesDirectlyWith: compiler.
            let arguments := self arguments collect: {:(ASTNode)each :: FunctionBytecodeAssemblerVectorOperand |
                each compileBytecodesDirectlyWith: compiler
            }.

            let result := compiler assembler temporary: #sendResult type: self analyzedType.
            compiler assembler sendTo: receiver receiverLookupType: receiverLookupType selector: selector arguments: arguments result: result.
            result
        } ifNil: {
            let receiver := self receiver compileBytecodesDirectlyWith: compiler.
            let selector := self selector compileBytecodesDirectlyWith: compiler.
            let arguments := self arguments collect: {:(ASTNode)each :: FunctionBytecodeAssemblerVectorOperand |
                each compileBytecodesDirectlyWith: compiler
            }.

            let result := compiler assembler temporary: #sendResult type: self analyzedType.
            compiler assembler sendTo: receiver selector: selector arguments: arguments result: result.
            result
        }
    }.
}.

ASTReturnNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand
        := compiler assembler
            return: (self expression compileBytecodesDirectlyWith: compiler);
            literal: nil
}.

ASTSequenceNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let result mutable => FunctionBytecodeAssemblerVectorOperand := compiler assembler literal: void.
        self expressions do: {:(ASTNode)expression :: Void |
            result := expression compileBytecodesDirectlyWith: compiler
        }.

        result
    }
}.

ASTTupleSlotNamedAtNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let tuple := self tupleExpression compileBytecodesDirectlyWith: compiler.
        let slot := compiler assembler literal: self boundSlot.
        let result := compiler assembler temporary: #slotNamedAt type: self analyzedType.
        self tupleExpression analyzedType isPointerLikeType ifTrue: {
            compiler assembler ref: tuple slotAt: slot into: result
        } ifFalse: {
            compiler assembler tuple: tuple slotAt: slot into: result
        }.

        result
    }.
}.

ASTTupleSlotNamedAtPutNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let tuple := self tupleExpression compileBytecodesDirectlyWith: compiler.
        let slot := compiler assembler literal: self boundSlot.
        let value := self valueExpression compileBytecodesDirectlyWith: compiler.

        self tupleExpression analyzedType isPointerLikeType ifTrue: {
            compiler assembler ref: tuple slotAt: slot put: value
        } ifFalse: {
            compiler assembler tuple: tuple slotAt: slot put: value
        }.

        compiler assembler literal: void
    }.
}.

ASTTupleSlotNamedReferenceAtNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let tuple := self tupleExpression compileBytecodesDirectlyWith: compiler.
        let slot := compiler assembler literal: self boundSlot.
        let result := compiler assembler temporary: #slotNamedReferenceAt type: self analyzedType.
        self tupleExpression analyzedType isPointerLikeType ifTrue: {
            compiler assembler ref: tuple slotReferenceAt: slot into: result
        } ifFalse: {
            compiler assembler tuple: tuple slotReferenceAt: slot into: result
        }.

        result
    }.
}.

ASTUseNamedSlotsOfNode extend: {
    public override eager method doCompileIntoBytecodeWith: (compiler: FunctionBytecodeDirectCompiler) ::=> FunctionBytecodeAssemblerVectorOperand := {
        let tuple := self tupleExpression compileBytecodesDirectlyWith: compiler.
        compiler setValue: tuple forBinding: self binding.
        compiler assembler literal: void
    }.
}.
