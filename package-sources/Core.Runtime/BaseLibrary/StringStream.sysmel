StringStream definition: {
    public eager method capacity => Size
        := storage ifNil: 0sz ifNotNil: storage size.

    public method ensureCapacityFor: (requiredExtraSize: Size) ::=> Void := {
        let requiredCapacity := size + requiredExtraSize.
        requiredCapacity <= self capacity ifTrue: {return: void}.

        let newCapacity := requiredCapacity asLargerPowerOfTwo max: 16sz.
        let oldStorage := storage.
        let newStorage := String basicAllocate: newCapacity.
        let currentSize := size.
        ObjectModel::isLogical() ifTrue: {
            let i mutable := 0sz.
            while: (i < currentSize) do: {
                newStorage char8At: i put: (oldStorage char8At: i)
            } continueWith: (i := i + 1sz).
        } ifFalse: {
            memcpy(newStorage __rawContentsPointer__, oldStorage __rawContentsPointer__, currentSize).
        }.

        storage := newStorage.
    }.

    public override method nextPut: character ::=> Void := {
        self ensureCapacityFor: 1sz.
        storage char8At: size put: (character uncheckedDownCastTo: Char8).
        size := size + 1sz.
    }.

    public override method nextPutAll: aCollection ::=> Void := {
        RawTuple::isBytes(aCollection) ifTrue: {
            self nextPutAllBytesOf: aCollection
        } ifFalse: {
            self ensureCapacityFor: aCollection size.
            super nextPutAll: aCollection
        }.
    }.

    public override method nextPutAllBytesOf: anObject ::=> Void := {
        let byteCount := RawTuple::byteSize(anObject).
        self ensureCapacityFor: byteCount.

        ObjectModel::isLogical() ifTrue: {
            let i mutable := 0sz.
            while: (i < byteCount) do: {
                storage at: size put: (RawTuple::byteSlotAt:)(anObject, i) asChar8.
                size := size + 1sz
            } continueWith: (i := i + 1sz).
        } ifFalse: {
            memcpy((self __slotNamedAt__: #storage) __rawContentsBytePointer__ + size asIntPointer reinterpretCastTo: Void pointer,
                anObject __rawContentsPointer__,
                byteCount).
            size := size + byteCount
        }
    }.

    public method asByteArray => ByteArray := {
        let byteArraySize := self size.
        let byteArray := ByteArray new: byteArraySize.

        let i mutable := 0sz.
        while: (i < byteArraySize) do: {
            byteArray uint8At: i put: (storage char8At: i) asUInt8
        } continueWith: (i := i + 1sz).

        byteArray
    }.

    public override method asString => String
        := size = 0sz ifTrue: "" ifFalse: (storage first: size).

    public method asSymbol => StringSymbol
        := size = 0sz ifTrue: #"" ifFalse: (self asString asSymbol).
}.
