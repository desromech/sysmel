TypeSlot
    withSelector: #hasAnyFlags: addMethod: {:(TypeSlot)self :(Bitflags)flags :: Boolean |
        ((self __slotNamedAt__: #flags) & flags) ~= 0bflgs
    } withEagerAnalysis;
    withSelector: #visibility addMethod: {:(TypeSlot)self :: Bitflags |
        ((self __slotNamedAt__: #flags) & TypeSlotFlags::VisibilityMask) >> TypeSlotFlags::VisibilityShift
    } withEagerAnalysis;
    withSelector: #isPrivate addMethod: {:(TypeSlot)self :: Boolean |
        self visibility = TypeSlotFlags::Private
    } withEagerAnalysis;
    withSelector: #isProtected addMethod: {:(TypeSlot)self :: Boolean |
        self visibility = TypeSlotFlags::Protected
    } withEagerAnalysis;
    withSelector: #isInternal addMethod: {:(TypeSlot)self :: Boolean |
        self visibility = TypeSlotFlags::Internal
    } withEagerAnalysis;
    withSelector: #isPublic addMethod: {:(TypeSlot)self :: Boolean |
        self visibility = TypeSlotFlags::Public
    } withEagerAnalysis;
    withSelector: #isReadonly addMethod: {:(TypeSlot)self :: Boolean |
        self hasAnyFlags: TypeSlotFlags::ReadOnly
    } withEagerAnalysis;
    withSelector: #getterSelector addMethod: {:(TypeSlot)self :: Symbol | self name };
    withSelector: #setterSelector addMethod: {:(TypeSlot)self :: Symbol | (self name asString -- ":") asSymbol };
    withSelector: #generateGetterWithReceiver:on: addMethod: {:(TypeSlot)self :(Type)ReceiverType :(Type)ownerType :: Void |
        let: #SlotType with: (self __slotNamedAt__: #type).
        ownerType withSelector: self getterSelector addMaybeVirtualMethod: ({:(ReceiverType)receiver :: SlotType |
            <keepSourceDefinition>
            (RawTuple::typeSlotAt:)(receiver, self)
        } makePure makeFinal makeAlwaysInline makeWithoutSourceDebugging recompileAndOptimize)
    };
    withSelector: #generateSetterWithReceiver:on: addMethod: {:(TypeSlot)self :(Type)ReceiverType :(Type)ownerType :: Void |
        let: #SlotType with: (self __slotNamedAt__: #type).
        ownerType withSelector: self setterSelector addMaybeVirtualMethod: ({:(ReceiverType)receiver :(SlotType)value :: Void |
            <keepSourceDefinition>
            (RawTuple::typeSlotAt:put:)(receiver, self, value)
        } makeFinal makeAlwaysInline makeWithoutSourceDebugging recompileAndOptimize)
    };
    withSelector: #generateAccessorsOn: addMethod: {:(TypeSlot)self :(Type)type :: Void |
        if: self isPublic then: {
            let: #receiverType with: type asReceiverType.
            receiverType ReceiverType::generateGetterFor: self on: type.

            if: self isReadonly not then: {
                receiverType ReceiverType::generateSetterFor: self on: type
            }
        }.
        void
    }.

Type
    withSelector: #ReceiverType::generateGetterFor:on: addMethod: {:(Type)self :(TypeSlot)typeSlot :(Type)ownerType :: Void |
        typeSlot generateGetterWithReceiver: self on: ownerType.
    } makeVirtual;
    withSelector: #ReceiverType::generateSetterFor:on: addMethod: {:(Type)self :(TypeSlot)typeSlot :(Type)ownerType :: Void |
        typeSlot generateSetterWithReceiver: self on: ownerType.
    } makeVirtual.
