public class ImageBuilderHIR_MIRTarget superclass: ImageBuilderHIRTarget; definition: {
    public override method buildTargetImageWith: (builder: ImageBuilder) ::=> ImageBuilderTargetImage := {
        let hirImage := ImageBuilderHIR_MIRImage new.
        
        let ssaMirCompilation := compilationTarget downCastTo: HIRMIRCompilationTarget.
        {|
            hirImage hirModule: ((HIRModuleCompilationContext forTarget: compilationTarget)
                    exclusiveCompilationMode: true;
                    compileFunctions: builder tracer tracedFunctions definitions: builder tracer tracedFunctionDefinitions;
                    exportMainEntryPointFunction: builder entryPointFunction;
                    finish).
        } printTimeToRunInMicrosecondsWithTitle: "HIR Module".

        {|
            hirImage hirModule performStandardOptimizations
        } printTimeToRunInMicrosecondsWithTitle: "HIR optimization".

        {|
            hirImage mirModule: (HIRModuleMIRCodeGenerator new
                initializeWithTarget: ssaMirCompilation mirCompilationTarget;
                compileHIRModule: hirImage hirModule patchingFunctions: builder tracer requiredTracedReflectiveFunctions definitions: builder tracer requiredTracedReflectiveFunctionDefinitions;
                finish).
        } printTimeToRunInMicrosecondsWithTitle: "MIR Module".

        {|
            hirImage mirModule performStandardOptimizations
        } printTimeToRunInMicrosecondsWithTitle: "MIR optimization".

        {|
            hirImage targetIR: (hirImage mirModule
                    baseObjectTracer: builder tracer;
                    asTargetIR).
        } printTimeToRunInMicrosecondsWithTitle: "Assembly code".

        {|
            hirImage targetIR performStandardOptimizations
        } printTimeToRunInMicrosecondsWithTitle: "Assembly code optimizations".

        printLine("Image data generated").
        hirImage
    }.

    public override method needsBytecode => Boolean
        := false.
}.

HIRMIRCompilationTarget definition: {
    public override method asImageBuilderTarget => ImageBuilderHIRTarget
        := ImageBuilderHIR_MIRTarget for: self
}.

MIRCompilationTarget definition: {
    public method asImageBuilderTarget => ImageBuilderHIRTarget
        := self asHIRCompilationTarget asImageBuilderTarget.
}.

AsmX86CompilationTarget definition: {
    public method asImageBuilderTarget => ImageBuilderHIRTarget
        := self asMirCompilationTarget asHIRCompilationTarget asImageBuilderTarget.
}.

AsmX86_64CompilationTarget definition: {
    public method asImageBuilderTarget => ImageBuilderHIRTarget
        := self asMirCompilationTarget asHIRCompilationTarget asImageBuilderTarget.
}.
