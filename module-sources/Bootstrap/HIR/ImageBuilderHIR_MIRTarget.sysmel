public class ImageBuilderHIR_MIRTarget superclass: ImageBuilderHIRTarget; definition: {
    public override method buildTargetImageWith: (builder: ImageBuilder) ::=> ImageBuilderTargetImage := {
        {|
            let traceHistogram := builder tracer objectTrace buildHistogram.
            IO::writeWholeFileNamedWithString("ObjectTraceHistogram.csv", traceHistogram fullPrintString)
        } printTimeToRunInMillisecondsWithTitle: "Trace histogram".


        let hirImage := ImageBuilderHIR_MIRImage new.
        
        let ssaMirCompilation := compilationTarget downCastTo: HIRMIRCompilationTarget.
        {|
            hirImage hirModule: ((HIRModuleCompilationContext forTarget: compilationTarget)
                    exclusiveCompilationMode: true;
                    virtualTableSetSource: builder tracer vtableSet;
                    compileFunctions: builder tracer tracedFunctions definitions: builder tracer tracedFunctionDefinitions;
                    exportMainEntryPointFunction: builder entryPointFunction;
                    finish).
        } printTimeToRunInMillisecondsWithTitle: "HIR Module".

        {|
            hirImage hirModule performStandardOptimizations
        } printTimeToRunInMillisecondsWithTitle: "HIR optimization".

        {|
            hirImage mirModule: (HIRModuleMIRCodeGenerator new
                initializeWithTarget: ssaMirCompilation mirCompilationTarget;
                compileHIRModule: hirImage hirModule patchingFunctions: builder tracer requiredTracedReflectiveFunctions definitions: builder tracer requiredTracedReflectiveFunctionDefinitions;
                finish).
        } printTimeToRunInMillisecondsWithTitle: "MIR Module".

        {|
            hirImage mirModule performStandardOptimizations
        } printTimeToRunInMillisecondsWithTitle: "MIR optimization".

        {|
            hirImage targetIR: (hirImage mirModule
                    baseObjectTracer: builder tracer;
                    asTargetIR).
        } printTimeToRunInMillisecondsWithTitle: "Assembly code".

        {|
            hirImage targetIR performStandardOptimizations
        } printTimeToRunInMillisecondsWithTitle: "Assembly code optimizations".

        printLine("Image data generated").
        hirImage
    }.
}.

HIRMIRCompilationTarget definition: {
    public override method asImageBuilderTarget => ImageBuilderHIRTarget
        := ImageBuilderHIR_MIRTarget for: self
}.

MIRCompilationTarget definition: {
    public method asImageBuilderTarget => ImageBuilderHIRTarget
        := self asHIRCompilationTarget asImageBuilderTarget.
}.

AsmCompilationTarget definition: {
    public abstract method asImageBuilderTarget => ImageBuilderHIRTarget
        := self subclassResponsibility.
}.

AsmX86CompilationTarget definition: {
    public override method asImageBuilderTarget => ImageBuilderHIRTarget
        := self asMirCompilationTarget asHIRCompilationTarget asImageBuilderTarget.
}.

AsmX86_64CompilationTarget definition: {
    public override method asImageBuilderTarget => ImageBuilderHIRTarget
        := self asMirCompilationTarget asHIRCompilationTarget asImageBuilderTarget.
}.

PlatformDescription definition: {
    public method asAsmCompilationTarget => AsmCompilationTarget := {
        let target => AsmCompilationTarget := AsmCompilationTarget perform: architectureName.
        target
            setupForPlatform: self;
            yourself
    }.

    public method asImageBuilderTarget => ImageBuilderHIRTarget
        := self asAsmCompilationTarget asImageBuilderTarget.
}.