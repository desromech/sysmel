public abstract class TestCase superclass: TestAsserter; definition: {
    __Meta__ definition: {
        public method withAllSubclassesRun ::=> Void := {
            self withAllSubtypesDo: {:(TestCase class)eachClass :: Void |
                eachClass isAbstract ifFalse: {
                    eachClass run
                }
            }
        }.

        public method run ::=> Void := {
            self testSelectors do: {:(Symbol)eachTestSelector :: Void |
                self run: eachTestSelector
            }
        }.

        public method run: (testSelector: Symbol) ::=> Void := {
            self new
                setUp;
                perform: testSelector;
                tearDown.
        }.

        public method testSelectors => Array := {
            let: #selectors with: OrderedCollection new.
            self withSupertypesDo: {:subtype :: Void |
                subtype selectorsDo: {:(Symbol)each :: Void |
                    let symbolString := each asString.
                    if: (symbolString beginsWith: "test") && (symbolString numSelectorArgs = 0sz) then: {
                        selectors add: each
                    }
                }
            }.

            selectors asArray
        }.
    }.

    public method setUp => Void := {

    }.

    public method tearDown => Void := {

    }.
}.
