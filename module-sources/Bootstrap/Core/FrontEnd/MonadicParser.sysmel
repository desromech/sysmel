public class MonadicParserStreamEndMarkClass definition: {
    let singleton := SelfType new.
    __Meta__ definition: {
        public method uniqueInstance => MonadicParserStreamEndMarkClass
            := singleton.
    }.
}.

public global MonadicParserStreamEndMark := MonadicParserStreamEndMarkClass uniqueInstance.

public class MonadicParserState definition: {
    public field sourceCollection => SequenceableCollection.
    public field sourceCode => SourceCode.
    public field position => Size.
    public field memoizationTable => Dictionary.

    __Meta__ definition: {
        public method for: (sourceCollection: SequenceableCollection) ::=> self
            := self new sourceCollection: sourceCollection; yourself.

        public method forSourceCode: (sourceCode: SourceCode) ::=> self
            := self new
                sourceCollection: sourceCode text;
                sourceCode: sourceCode;
                yourself.
    }.

    public inline method memento => Size
        := position.

    public inline method restoreMemento: (memento: Size) ::=> Void := {
        position := memento
    }.

    public method remainingSize => Size
        := position <= sourceCollection size
            ifTrue: sourceCollection size - position
            ifFalse: 0sz.

    public method peek: (offset: Size) ::=> AnyValue := {
        let peekPosition := position + offset.
        peekPosition < sourceCollection size
            ifTrue: (sourceCollection at: peekPosition)
            ifFalse: MonadicParserStreamEndMark
    }.

    public method peek ::=> AnyValue
        := self peek: 0sz.

    public method advance: (increment: Size) ::=> Void := {
        position := position + increment min: sourceCollection size
    }.

    public method advance ::=> Void
        := self advance: 1sz.

    public method atEnd => Boolean
        := position >= sourceCollection size.

    public virtual method sourcePositionFrom: (startIndex: Size) until: (endIndex: Size) ::=> SourcePosition
        := SourcePosition for: sourceCode startIndex: startIndex asUInt32 endIndex: endIndex asUInt32.

    public virtual method sourcePositionUntilEnd => SourcePosition
        := SourcePosition for: sourceCode startIndex: position asUInt32 endIndex: sourceCollection size asUInt32.

    public virtual method sourcePositionAtEnd => SourcePosition
        := SourcePosition for: sourceCode startIndex: sourceCollection size asUInt32 endIndex: sourceCollection size asUInt32.
}.

public abstract class MonadicParserResult superclass: Object; definition: {
    public field state => MonadicParserState.
    public field value.
    public field startPosition => Size.
    public field endPosition => Size.

    public abstract method isError => Boolean := false.
    public method isValid => Boolean := self isError not.

    public method sourceValue => SequenceableCollection
        := state sourceCollection copyFrom: startPosition until: endPosition.

    public method sourceStringValue => String
        := self sourceValue downCastTo: String.

    public method sourceStringValueWithoutFirst: (startTrim: Size) ::=> String
        := (state sourceCollection copyFrom: startPosition + startTrim until: endPosition) downCastTo: String.

    public method sourceStringValueWithoutFirst: (startTrim: Size) last: (endTrim: Size)::=> String
        := (state sourceCollection copyFrom: startPosition + startTrim until: endPosition - endTrim) downCastTo: String.

    public method sourcePosition => SourcePosition
        := state sourcePositionFrom: startPosition until: endPosition.

    public method sourcePositionWithoutFirst: (startTrim: Size) last: (endTrim: Size) ::=> SourcePosition
        := state sourcePositionFrom: startPosition + startTrim until: endPosition - endTrim.

    public override method printOn: (stream: Stream) ::=> Void := {
        stream print: startPosition; nextPut: '-'c8; print: endPosition; nextPutAll: ": "; print: value
    }.

    public virtual method errorMessage => String := "No an error".
}.

public class MonadicParserErrorResult superclass: MonadicParserResult; definition: {
    public field message => String.
    public field messageTemplate => String.
    public field messageTemplateParameters => Array.
    
    public abstract method isError => Boolean := true.

    public override method errorMessage => String := {
        message ifNil: {
            message := messageTemplate
                ifNil: ("Unknown error")
                ifNotNil: (messageTemplate)
        }.
        message.
    }.

    public override method printOn: (stream: Stream) ::=> Void := {
        stream print: startPosition; nextPut: '-'c8; print: endPosition; nextPutAll: ": "; nextPutAll: message
    }.
}.

public abstract class MonadicParser definition: {
    __Meta__ extend: {
        public method end => MonadicParser
            := MonadicEndParser new.

        public method any => MonadicParser
            := MonadicAnyParser new.

        public method predicate: predicate errorMessage: (errorMessage: String) ::=> MonadicParser
            := MonadicPredicateParser new
                predicate: predicate;
                errorMessage: errorMessage;
                yourself.

        public method white ::=> MonadicParser
            := self predicate: {:(Char8)character :: Boolean | character <= ' 'c8 } errorMessage: "Expected a whitespace.".

        public method digit ::=> MonadicParser
            := self predicate: {:(Char8)character :: Boolean | ('0'c8 <= character) && (character <= '9'c8) } errorMessage: "Expected a digit.".

        public method alpha ::=> MonadicParser
            := self predicate: {:(Char8)character :: Boolean |
                (('A'c8 <= character) && (character <= 'Z'c8)) ||
                (('a'c8 <= character) && (character <= 'z'c8))
            } errorMessage: "Expected an alphabetic character.".

        public method alphaNum ::=> MonadicParser
            := self predicate: {:(Char8)character :: Boolean |
                (('A'c8 <= character) && (character <= 'Z'c8)) ||
                (('a'c8 <= character) && (character <= 'z'c8)) ||
                (('0'c8 <= character) && (character <= '9'c8))
            } errorMessage: "Expected an alphanumeric character.".

        public method identifierStart ::=> MonadicParser
            := self predicate: {:(Char8)character :: Boolean |
                (('A'c8 <= character) && (character <= 'Z'c8)) ||
                (('a'c8 <= character) && (character <= 'z'c8)) ||
                (character = '_'c8)
            } errorMessage: "Expected an alphabetic character.".

        public method identifierMiddle ::=> MonadicParser
            := self predicate: {:(Char8)character :: Boolean |
                (('A'c8 <= character) && (character <= 'Z'c8)) ||
                (('a'c8 <= character) && (character <= 'z'c8)) ||
                (('0'c8 <= character) && (character <= '9'c8)) ||
                (character = '_'c8)
            } errorMessage: "Expected an alphanumeric character.".
    }.

    public field isOmmittingValue => Boolean.

    public method withoutValue => MonadicParser := {
        isOmmittingValue := true.
        self
    }.

    public method parse: (sourceCollection: SequenceableCollection) ::=> MonadicParserResult
        := self parseWithState: (MonadicParserState for: sourceCollection).

    public method parseSourceCode: (sourceCode: SourceCode) ::=> MonadicParserResult
        := self parseWithState: (MonadicParserState forSourceCode: sourceCode).

    public abstract method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult
        := self subclassResponsibility.

    public method makeResult: value withState: (state: MonadicParserState) ::=> MonadicParserResult
        := MonadicParserResult new
            state: state;
            value: value;
            startPosition: state memento;
            endPosition: state memento;
            yourself.

    public method makeResultWithState: (state: MonadicParserState) ::=> MonadicParserResult
        := MonadicParserResult new
            state: state;
            startPosition: state memento;
            endPosition: state memento;
            yourself.

    public method makeResult: value from: (startPosition: Size) withState: (state: MonadicParserState) ::=> MonadicParserResult
        := MonadicParserResult new
            state: state;
            value: value;
            startPosition: startPosition;
            endPosition: state memento;
            yourself.

    public method makeResultFrom: (startPosition: Size) withState: (state: MonadicParserState) ::=> MonadicParserResult
        := MonadicParserResult new
            state: state;
            startPosition: startPosition;
            endPosition: state memento;
            yourself.

    public method makeError: (message: String) withState: (state: MonadicParserState) ::=> MonadicParserResult
        := MonadicParserErrorResult new
            state: state;
            message: message;
            startPosition: state memento;
            endPosition: state memento;
            yourself.

    public method makeError: (messageTemplate: String) formatWith: (messageTemplateParameters: Array) withState: (state: MonadicParserState) ::=> MonadicParserResult
        := MonadicParserErrorResult new
            state: state;
            messageTemplate: messageTemplate;
            messageTemplateParameters: messageTemplateParameters;
            startPosition: state memento;
            endPosition: state memento;
            yourself.

    public virtual method -- (nextParser: MonadicParser) ::=> MonadicParser
        := MonadicCombinatorSequenceParser new
            parsers: (Array with: self with: nextParser);
            yourself.

    public virtual method | (nextParser: MonadicParser) ::=> MonadicParser
        := MonadicCombinatorAlternativesParser new
            parsers: (Array with: self with: nextParser);
            yourself.

    public virtual method memoized => MonadicParser 
        := MonadicMemoizedDelegateParser new
            parser: self;
            yourself.

    public method optional => MonadicParser
        := MonadicCombinatorOptionalParser new
            parser: self;
            yourself.

    public method star => MonadicParser
        := MonadicCombinatorRepetitionParser new
            parser: self;
            yourself.

    public method plus => MonadicParser
        := MonadicCombinatorRepetitionParser new
            parser: self;
            minimalCount: 1sz;
            yourself.
    public method plusN: (minimalCount: Size) ::=> MonadicParser
        := MonadicCombinatorRepetitionParser new
            parser: self;
            minimalCount: minimalCount;
            yourself.

    public method negate => MonadicParser
        := MonadicCombinatorNegateParser new
            parser: self;
            yourself.

    public method not => MonadicParser
        := MonadicCombinatorNotParser new
            parser: self;
            yourself.

    public method trim: (trimmingParser: MonadicParser) ::=> MonadicParser
        := MonadicCombinatorTrimParser new
            trimmingParser: trimmingParser;
            parser: self;
            yourself.

    public method ==> (valueComputationBlock: (MonadicParserResult) => AnyValue) ::=> MonadicParser
        := MonadicValueComputationParser new
            parser: self;
            valueComputationBlock: valueComputationBlock;
            yourself.
}.

public class MonadicEpsilonParser superclass: MonadicParser; definition: {
    public override method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult
        := self makeResult: nil withState: state.
}.

public class MonadicEndParser superclass: MonadicParser; definition: {
    public override method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult := {
        state atEnd ifTrue: {
            self makeResult: nil withState: state. 
        } ifFalse: {
            self makeError: "Expected the end." withState: state.
        }
    }
}.

public class MonadicElementParser superclass: MonadicParser; definition: {
    public field expectedElement => AnyValue.

    public override method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult := {
        let startPosition := state memento.
        state atEnd not && (expectedElement = state peek) ifTrue: {
            state advance.
            self makeResultFrom: startPosition withState: state.
        } ifFalse: {
            self makeError: "Expected {0}." formatWith: (Array with: expectedElement) withState: state.
        }.
    }
}.

AnyValue extend: {
    public method asMonadicElementParser => MonadicElementParser
        := MonadicElementParser new expectedElement: self; yourself
}.

public class MonadicPredicateParser superclass: MonadicParser; definition: {
    public field predicate.
    public field errorMessage => String.

    __Meta__ definition: {
        public method with: (predicate: (AnyValue => Boolean)) ::=> self
            := self new predicate: predicate; yourself
    }.

    public override method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult := {
        let startPosition := state memento.
        state atEnd not && predicate(state peek) ifTrue: {
            state advance.
            self makeResultFrom: startPosition withState: state
        } ifFalse: {
            self makeError: errorMessage withState: state.
        }
    }.
}.

public class MonadicAnyParser superclass: MonadicParser; definition: {
    public override method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult := {
        let startPosition := state memento.
        state atEnd ifTrue: {
            self makeError: "Unexpected end." withState: state.
        } ifFalse: {
            let value := state peek.
            state advance.
            self makeResult: value from: startPosition withState: state
        }
    }.
}.

public class MonadicSequenceParser superclass: MonadicParser; definition: {
    public field expectedSequence => SequenceableCollection.

    public override method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult := {
        let startPosition := state memento.
        (expectedSequence size <= state remainingSize) && (expectedSequence allSatisfyWithIndex: {:expectedElement :(Size)index :: Boolean |
            expectedElement = (state peek: index)
        }) ifTrue: {
            state advance: expectedSequence size.
            self makeResultFrom: startPosition withState: state.
        } ifFalse: {
            self makeError: "Expected {0}" formatWith: (Array with: expectedSequence) withState: state.
        }.
    }
}.

SequenceableCollection extend: {
    public method asMonadicParser => MonadicParser
        := MonadicSequenceParser new expectedSequence: self; yourself.
}.

public class MonadicSetParser superclass: MonadicParser; definition: {
    public field expectedSet => Collection.

    public override method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult := {
        let startPosition := state memento.
        state atEnd not && (expectedSet includes: state peek) ifTrue: {
            state advance.
            self makeResultFrom: startPosition withState: state
        } ifFalse: {
            self makeError: "Element not in set." withState: state.
        }
    }
}.

Collection extend: {
    public method asMonadicSetParser => MonadicParser
        := MonadicSetParser new expectedSet: self; yourself.
}.

public class MonadicCombinatorSequenceParser superclass: MonadicParser; definition: {
    public field parsers => Array.

    public override method -- (nextParser: MonadicParser) ::=> MonadicParser
        := MonadicCombinatorSequenceParser new
            parsers: parsers -- (Array with: nextParser);
            yourself.

    public override method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult := {
        let startPosition := state memento.
        let parserCount := parsers size.
        let i mutable := 0sz.
        let resultValue => Array := isOmmittingValue ifTrue: nil ifFalse: (Array new: parserCount).
        while: (i < parserCount) do: {
            let parser => MonadicParser := parsers at: i.
            let parserResult := parser parseWithState: state.
            parserResult isError ifTrue: {return: parserResult}.

            isOmmittingValue ifFalse: {
                resultValue at: i put: parserResult value.
            }
        } continueWith: (i := i + 1sz).

        self makeResult: resultValue from: startPosition withState: state.
    }.
}.

public class MonadicCombinatorAlternativesParser superclass: MonadicParser; definition: {
    public field parsers => Array.

    public override method | (nextParser: MonadicParser) ::=> MonadicParser
        := MonadicCombinatorAlternativesParser new
            parsers: parsers -- (Array with: nextParser);
            yourself.

    public override method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult := {
        let startPosition := state memento.
        let parserResult mutable => MonadicParserResult := nil.
        let i mutable := 0sz.
        let parserCount := parsers size.
        while: (i < parserCount) do: {
            let parser => MonadicParser := parsers at: i.
            parserResult := parser parseWithState: state.
            parserResult isValid ifTrue: {return: parserResult}.
            state restoreMemento: startPosition.
        } continueWith: (i := i + 1sz).

        parserResult
    }.
}.

public class MonadicCombinatorOptionalParser superclass: MonadicParser; definition: {
    public field parser => MonadicParser.

    public override method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult := {
        let startPosition := state memento.
        let parseResult := parser parseWithState: state.
        parseResult isError ifTrue: {
            state restoreMemento: startPosition.
            self makeResultWithState: state.
        } ifFalse: {
            parseResult
        }
    }.
}.

public class MonadicCombinatorNegateParser superclass: MonadicParser; definition: {
    public field parser => MonadicParser.

    public override method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult := {
        let startPosition := state memento.
        let parseResult := parser parseWithState: state.
        parseResult isValid ifTrue: {
            self makeError: "Unexpected" withState: state
        } ifFalse: {
            state restoreMemento: startPosition.
            state advance.
            self makeResultWithState: state
        }
    }.
}.

public class MonadicCombinatorNotParser superclass: MonadicParser; definition: {
    public field parser => MonadicParser.

    public override method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult := {
        let startPosition := state memento.
        let parseResult := parser parseWithState: state.
        parseResult isValid ifTrue: {
            self makeError: "Unexpected" withState: state
        } ifFalse: {
            state restoreMemento: startPosition.
            self makeResultWithState: state
        }
    }.
}.

public class MonadicCombinatorRepetitionParser superclass: MonadicParser; definition: {
    public field parser => MonadicParser.
    public field minimalCount => Size.

    public override method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult := {
        let startPosition := state memento.
        let currentPosition mutable := startPosition.

        let resultValue => OrderedCollection := isOmmittingValue ifTrue: nil ifFalse: OrderedCollection new.
        let nextParseResult mutable => MonadicParserResult := nil.
        let i mutable := 0sz.
        while: (i < minimalCount) do: {
            nextParseResult := parser parseWithState: state.
            nextParseResult isError ifTrue: {return: nextParseResult}.
            isOmmittingValue ifFalse: {
                resultValue add: nextParseResult value.
            }.
        } continueWith: (i := i + 1sz).

        currentPosition := state memento.
        while: ((nextParseResult := parser parseWithState: state) isValid) do: {
            isOmmittingValue ifFalse: {
                resultValue add: nextParseResult value.
            }.
            currentPosition := state memento.
        }.

        state restoreMemento: currentPosition.
        self makeResult: resultValue from: startPosition withState: state
    }.
}.

public class MonadicCombinatorTrimParser superclass: MonadicParser; definition: {
    public field trimmingParser => MonadicParser.
    public field parser => MonadicParser.

    public override method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult := {
        {
            let trimLeftPosition := state memento.
            let trimLeftResult := trimmingParser parseWithState: state.
            trimLeftResult isError ifTrue: {
                state restoreMemento: trimLeftPosition
            }.
        }.

        let parseResult := parser parseWithState: state.
        parseResult isError ifTrue: {return: parseResult}.

        {
            let trimRightPosition := state memento.
            let trimRightResult := trimmingParser parseWithState: state.
            trimRightResult isError ifTrue: {
                state restoreMemento: trimRightPosition
            }.
        }.

        parseResult
    }.
}.

public class MonadicValueComputationParser superclass: MonadicParser; definition: {
    public field parser => MonadicParser.
    public field valueComputationBlock => ((MonadicParserResult) => AnyValue).

    public override method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult := {
        let parseResult := parser parseWithState: state.
        parseResult isValid ifTrue: {
            parseResult copy
                value: valueComputationBlock(parseResult);
                yourself
        } ifFalse: {
            parseResult
        }
    }
}.

public class MonadicDelegateParser superclass: MonadicParser; definition: {
    public field parser => MonadicParser.

    public override method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult
        := parser parseWithState: state.

    (public method := (newParser: MonadicParser)) ::=> Void := {
        parser := newParser
    }.
}.

public class MonadicMemoizedDelegateParser superclass: MonadicDelegateParser; definition: {
    public override method memoized => MonadicParser
        := self.

    public override method parseWithState: (state: MonadicParserState) ::=> MonadicParserResult := {
        state parseWithMemoizationOf: parser
    }.
}.

MonadicParserState extend: {
    public method validMemoizationTable => Dictionary
        := memoizationTable ifNil: {memoizationTable := Dictionary new}.

    public method parseWithMemoizationOf: (parser: MonadicParser) ::=> MonadicParserResult := {
        let memoizedResult => MonadicParserResult := self validMemoizationTable at: (Array with: position with: parser) ifAbsentPut: {:: MonadicParserResult |
            parser parseWithState: self
        }.

        position := memoizedResult endPosition.
        memoizedResult
    }.
}.
