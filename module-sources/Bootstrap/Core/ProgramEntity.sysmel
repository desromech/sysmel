Symbol extend: {
    public method asCheckedEntryPointNameSymbol => Symbol
        := self error: "Not a function name symbol".

    public method asUncheckedEntryPointNameSymbol => Symbol
        := self error: "Not a function name symbol".

    public method asCapturelessUncheckedEntryPointNameSymbol => Symbol
        := self error: "Not a function name symbol".
}.

public final class ProgramEntityQualifiedNameSymbol superclass: Symbol; definition: {
    public field programEntity => ProgramEntity.

    __Meta__ definition:{
        public method for: (programEntity: ProgramEntity) ::=> self
            := self new programEntity: programEntity; yourself.
    }.

    public method printOn: (stream: Stream) ::=> Void := {
        programEntity printQualifiedSymbolNameOn: stream.
    }.

    public method asCapturelessUncheckedEntryPointNameSymbol => Symbol
        := programEntity capturelessUncheckedEntryPointNameSymbol.

    public method asCheckedEntryPointNameSymbol => Symbol
        := programEntity checkedEntryPointNameSymbol.

    public method asUncheckedEntryPointNameSymbol => Symbol
        := programEntity uncheckedEntryPointNameSymbol.
}.

public final class FunctionCheckedEntryPointSymbol superclass: Symbol; definition: {
    public field function => FunctionDefinition.

    __Meta__ definition:{
        public method for: (function: FunctionDefinition) ::=> self
            := self new function: function; yourself.
    }.

    public method printOn: (stream: Stream) ::=> Void := {
        function printQualifiedSymbolNameOn: stream.
        stream nextPutAll: ".checked".
    }.
}.

public final class FunctionCapturelessUncheckedEntryPointSymbol superclass: Symbol; definition: {
    public field function => FunctionDefinition.

    __Meta__ definition:{
        public method for: (function: FunctionDefinition) ::=> self
            := self new function: function; yourself.
    }.

    public method printOn: (stream: Stream) ::=> Void := {
        function printQualifiedSymbolNameOn: stream.
        stream nextPutAll: ".captureless".
    }.
}.

public final class FunctionUncheckedEntryPointSymbol superclass: Symbol; definition: {
    public field function => FunctionDefinition.

    __Meta__ definition:{
        public method for: (function: FunctionDefinition) ::=> self
            := self new function: function; yourself.
    }.

    public method printOn: (stream: Stream) ::=> Void := {
        function printQualifiedSymbolNameOn: stream.
        stream nextPutAll: ".unchecked".
    }.
}.

ProgramEntity definition: {
    public method programEntityKindName => Symbol
        := self class name.

    public method printSymbolNameOn: (stream: Stream) ::=> Void := {
        name ifNotNil: (stream string: name).
        stream nextPut: '#'; string: self programEntityKindName.
        stream nextPut: '_'.
        self identityHash printHexOn: stream.
    }.

    public method printQualifiedSymbolNameOn: (stream: Stream) ::=> Void := {
        owner ifNotNil: {
            owner printQualifiedSymbolNameOn: stream.
            stream nextPut: '.'.
        }.

        self printSymbolNameOn: stream
    }.

    public method printOn: (stream: Stream) ::=> Void := {
        self printQualifiedSymbolNameOn: stream
    }.

    public method programEntityQualifiedNameSymbol => ProgramEntityQualifiedNameSymbol
        := ProgramEntityQualifiedNameSymbol for: self.

    public method capturelessUncheckedEntryPointNameSymbol => FunctionCapturelessUncheckedEntryPointSymbol
        := self error: "Not a function definition".

    public method checkedEntryPointNameSymbol => FunctionCheckedEntryPointSymbol
        := self error: "Not a function definition".

    public method uncheckedEntryPointNameSymbol => FunctionUncheckedEntryPointSymbol
        := self error: "Not a function definition".
}.

Type definition: {
    public method programEntityKindName => Symbol
        := #T.
}.

Namespace definition: {
    public method programEntityKindName => Symbol
        := #N.
}.

Function definition: {
    public method programEntityKindName => Symbol
        := #C.
}.

FunctionDefinition definition: {
    public method programEntityKindName => Symbol
        := #F.

    public method checkedEntryPointNameSymbol => FunctionCheckedEntryPointSymbol
        := FunctionCheckedEntryPointSymbol for: self.

    public method uncheckedEntryPointNameSymbol => FunctionUncheckedEntryPointSymbol
        := FunctionUncheckedEntryPointSymbol for: self.

    public method capturelessUncheckedEntryPointNameSymbol => Symbol
        := FunctionCapturelessUncheckedEntryPointSymbol for: self.
}.
