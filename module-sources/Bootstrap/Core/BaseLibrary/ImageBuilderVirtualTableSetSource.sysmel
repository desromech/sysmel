public final class ImageBuilderVirtualTableSetSource superclass: VirtualTableSetSource; definition: {
    private field virtualTableLayouts => MethodDictionary.
    private field virtualTables => MethodDictionary.

    public override method initialize => Void := {
        virtualTableLayouts := MethodDictionary new.
        virtualTables := MethodDictionary new.
    }.

    public override method getVirtualTableLayoutFor: (type: Type) ::=> VirtualTableLayout := {
        virtualTableLayouts at: type ifAbsent: {:: VirtualTableLayout |
            let newLayout := VirtualTableLayout new.
            virtualTableLayouts at: type put: newLayout.

            let supertype := type supertype.
            newLayout setSupertypeLayout: (supertype ifNotNil: { self getVirtualTableLayoutFor: supertype }).

            let virtualMethodSelectorList := type virtualMethodSelectorList.
            virtualMethodSelectorList ifNotNil: {
                newLayout addSelectors: virtualMethodSelectorList
            }.

            newLayout
        }
    }.

    public override method getVirtualTableFor: (type: Type) ::=> VirtualTable := {
        virtualTables at: type ifAbsentPut: {:: VirtualTable |
            let layout := self getVirtualTableLayoutFor: type.
            layout buildVirtualTableFor: type
        }
    }.

    public override method getVirtualTableLayoutOrNilOf: (type: Untyped) ::=> VirtualTableLayout
        := virtualTableLayouts untypedAtOrNil: type.

    public override method getVirtualTableOrNilOf: (type: Untyped) ::=> VirtualTable
        := virtualTables untypedAtOrNil: type.
}.
