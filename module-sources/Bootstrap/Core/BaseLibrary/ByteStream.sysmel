ByteStream definition: {
    public eager method capacity => Size
        := storage ifNil: 0sz ifNotNil: storage size.

    public method ensureCapacityFor: (requiredExtraSize: Size) ::=> Void := {
        let requiredCapacity := size + requiredExtraSize.
        requiredCapacity <= self capacity ifTrue: {return: void}.

        let newCapacity := requiredCapacity asLargerPowerOfTwo max: 16sz.
        let newStorage := ByteArray basicAllocate: newCapacity.
        let currentSize := size.
        let i mutable := 0sz.
        while: (i < currentSize) do: {
            newStorage at: i put: (storage at: i)
        } continueWith: (i := i + 1sz).

        storage := newStorage.
    }.

    public override method nextPut: (byte: UInt8) ::=> Void := {
        self ensureCapacityFor: 1sz.
        storage uint8At: size put: byte.
        size := size + 1sz.
    }.

    public override method nextPutAll: (bytes: ByteArray) ::=> Void := {
        let byteCount := bytes size.
        self ensureCapacityFor: byteCount.

        let i mutable := 0sz.
        while: (i < byteCount) do: {
            storage uint8At: size put: (bytes at: i).
            size := size + 1sz
        } continueWith: (i := i + 1sz).
    }.

    public method nextPutString: (string: String) ::=> Void := {
        let stringSize := string size.
        self ensureCapacityFor: stringSize.

        let i mutable := 0sz.
        while: (i < stringSize) do: {
            storage char8At: size put: (string char8At: i).
            size := size + 1sz
        } continueWith: (i := i + 1sz).
    }.

    public method asByteArray => ByteArray
        := size = 0sz ifTrue: #[] ifFalse: (storage first: size).

    public override method asString => String := {
        let stringSize := self size.
        let string := String new: stringSize.

        let i mutable := 0sz.
        while: (i < stringSize) do: {
            string char8At: i put: (storage char8At: i).
        } continueWith: (i := i + 1sz).

        string
    }.

    public method asSymbol => StringSymbol
        := self asString asSymbol.
}.
