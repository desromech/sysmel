Set definition: {
    public eager method scanFor: key hashFunction: hashFunction equalsFunction: equalsFunction ::=> IntPointer := {
        storage ifNil: { return: -1iptr }.

        let capacity := storage size.
        let keyHash => Size := hashFunction(key).
        let naturalIndex := keyHash % capacity.

        let i mutable := naturalIndex.
        while: (i < capacity) do: {
            let element := storage untypedAt: i.
            if: (Untyped::==)(element, __hashtableEmptyElement__) || equalsFunction(key, element) then: {
                return: i asIntPointer
            }
        } continueWith: (i := i + 1sz).

        i := 0sz.
        while: (i < capacity) do: {
            let element := storage untypedAt: i.
            if: (Untyped::==)(element, __hashtableEmptyElement__) || equalsFunction(key, element) then: {
                return: i asIntPointer
            }
        } continueWith: (i := i + 1sz).

        -1iptr
    }.

    public virtual eager method scanFor: key ::=> IntPointer := {
        storage ifNil: { return: -1iptr }.

        let capacity := storage size.
        let naturalIndex := key hash % capacity.

        let i mutable := naturalIndex.
        while: (i < capacity) do: {
            let element := storage untypedAt: i.
            if: (Untyped::==)(element, __hashtableEmptyElement__) || key = element then: {
                return: i asIntPointer
            }
        } continueWith: (i := i + 1sz).

        i := 0sz.
        while: (i < capacity) do: {
            let element := storage untypedAt: i.
            if: (Untyped::==)(element, __hashtableEmptyElement__) || key = element then: {
                return: i asIntPointer
            }
        } continueWith: (i := i + 1sz).

        -1iptr
    }.

    public final eager method capacity => Size
        := storage ifNil: 0sz ifNotNil: storage size.

    public eager method addNoCheck: (element: Untyped) ::=> Void := {
        let index mutable := self scanFor: element.
        self assert: index >= 0iptr.
        storage untypedAt: index asSize put: element
    }.

    public eager method increaseCapacity => Void := {
        let oldCapacity := self capacity.
        let newCapacity := oldCapacity * 2sz max: 8sz.
        let oldStorage := storage.
        storage := Array new: newCapacity.
        storage atAllPut: __hashtableEmptyElement__.

        let i mutable := 0sz.
        while: (i < oldCapacity) do: {
            let oldElement := oldStorage untypedAt: i.
            (Untyped::==)(oldElement, __hashtableEmptyElement__) ifFalse: {
                self addNoCheck: oldElement
            }
        } continueWith: (i := i + 1sz)
    }.

    public eager method checkCapacityIncreaseCondition => Void := {
        let capacityThreshold := self capacity * 4sz / 5sz.
        self size >= capacityThreshold ifTrue: {
            self increaseCapacity
        }
    }.

    public virtual eager method add: element ::=> Void := {
        let index mutable := self scanFor: element.
        index < 0iptr ifTrue: {
            self increaseCapacity.
            index := self scanFor: element.
            self assert: index >= 0iptr
        }.
        
        let isNewElement := (Untyped::==)(storage untypedAt: index asSize , __hashtableEmptyElement__).
        storage at: index asSize put: element.
        isNewElement ifTrue: {
            size := size + 1sz.
            self checkCapacityIncreaseCondition
        }
    }.

    public virtual eager method includes: element ::=> Boolean := {
        let index := self scanFor: element.
        (index >= 0iptr) && (element = (storage untypedAt: index asSize))
    }.

    public method atOrNil: element withHashFunction: hashFunction equalsFunction: equalsFunction := {
        let index := self scanFor: element hashFunction: hashFunction equalsFunction: equalsFunction.
        (index >= 0iptr) ifTrue: {
            let foundElement := storage untypedAt: index asSize.
            (Untyped::==)(foundElement, __hashtableEmptyElement__)
                ifTrue: nil
                ifFalse: foundElement.
        } ifFalse: {
            nil
        }
    }.

    public method addAll: (collection: Collection) ::=> Void := {
        collection do: {:each :: Void | self add: each}
    }.

    public method fixCollisionsFrom: (start: Size) ::=> Void := {
        let capacity := storage size.
        let index mutable := (start + 1sz) % capacity.

        while: (Untyped::~~)((let element := storage untypedAt: index), __hashtableEmptyElement__) do: {
            let newIndex := self scanFor: element.
            newIndex asSize = index ifFalse: {
                storage swap: index with: newIndex asSize
            }.

            index := (index + 1sz) % capacity
        }.
    }.

    public method remove: element ifAbsent: aBlock := {
        let index := self scanFor: element.
        index < 0iptr ifTrue: {return: aBlock()}.

        let foundElement := storage untypedAt: index asSize.
        (Untyped::==)(foundElement, __hashtableEmptyElement__) ifTrue: {
            return: aBlock()
        }.
        
        storage untypedAt: index asSize put: __hashtableEmptyElement__.
        size := size - 1sz.
        self fixCollisionsFrom: index asSize.

        foundElement
    }.

    public method remove: element ::=> Void
        := self remove: element ifAbsent: {| self error: "Element not found"}.
}.
