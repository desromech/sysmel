Symbol extend: {
    public virtual method asCheckedEntryPointNameSymbol => Symbol
        := self error: "Not a function name symbol".

    public virtual method asUncheckedEntryPointNameSymbol => Symbol
        := self error: "Not a function name symbol".

    public virtual method asCapturelessUncheckedEntryPointNameSymbol => Symbol
        := self error: "Not a function name symbol".
}.

public final class ProgramEntityQualifiedNameSymbol superclass: Symbol; definition: {
    public field programEntity => ProgramEntity.

    __Meta__ definition:{
        public method for: (programEntity: ProgramEntity) ::=> self
            := self new programEntity: programEntity; yourself.
    }.

    public override method printOn: (stream: Stream) ::=> Void := {
        programEntity printQualifiedSymbolNameOn: stream.
    }.

    public override method asCapturelessUncheckedEntryPointNameSymbol => Symbol
        := programEntity capturelessUncheckedEntryPointNameSymbol.

    public override method asCheckedEntryPointNameSymbol => Symbol
        := programEntity checkedEntryPointNameSymbol.

    public override method asUncheckedEntryPointNameSymbol => Symbol
        := programEntity uncheckedEntryPointNameSymbol.
}.

public final class FunctionCheckedEntryPointSymbol superclass: Symbol; definition: {
    public field function => FunctionDefinition.

    __Meta__ definition:{
        public method for: (function: FunctionDefinition) ::=> self
            := self new function: function; yourself.
    }.

    public override method printOn: (stream: Stream) ::=> Void := {
        function printQualifiedSymbolNameOn: stream.
        stream nextPutAll: ".checked".
    }.
}.

public final class FunctionCapturelessUncheckedEntryPointSymbol superclass: Symbol; definition: {
    public field function => FunctionDefinition.

    __Meta__ definition:{
        public method for: (function: FunctionDefinition) ::=> self
            := self new function: function; yourself.
    }.

    public override method printOn: (stream: Stream) ::=> Void := {
        function printQualifiedSymbolNameOn: stream.
        stream nextPutAll: ".captureless".
    }.
}.

public final class FunctionUncheckedEntryPointSymbol superclass: Symbol; definition: {
    public field function => FunctionDefinition.

    __Meta__ definition:{
        public method for: (function: FunctionDefinition) ::=> self
            := self new function: function; yourself.
    }.

    public override method printOn: (stream: Stream) ::=> Void := {
        function printQualifiedSymbolNameOn: stream.
        stream nextPutAll: ".unchecked".
    }.
}.

ProgramEntity definition: {
    public virtual method programEntityKindName => Symbol
        := self class name.

    public method printQualifiedSymbolNameOn: (stream: Stream) ::=> Void := {
        self printQualifiedNameOn: stream.
        stream nextPutAll: ".S"; string: self programEntityKindName.
        stream nextPut: '_'c8.
        self identityHash asUInt32 printHexOn: stream.
    }.

    public virtual method printQualifiedNameOn: (stream: Stream) ::=> Void := {
        owner ifNotNil: {
            owner printQualifiedNameOn: stream.
            stream nextPut: '.'c8
        }.

        self name ifNotNil: (stream string: self name)
    }.

    public method qualifiedName => String := {
        let stream := StringStream new.
        self printQualifiedNameOn: stream.
        stream asString
    }.

    public method programEntityQualifiedNameSymbol => ProgramEntityQualifiedNameSymbol
        := ProgramEntityQualifiedNameSymbol for: self.

    public abstract method capturelessUncheckedEntryPointNameSymbol => FunctionCapturelessUncheckedEntryPointSymbol
        := self error: "Not a function definition".

    public abstract method checkedEntryPointNameSymbol => FunctionCheckedEntryPointSymbol
        := self error: "Not a function definition".

    public abstract method uncheckedEntryPointNameSymbol => FunctionUncheckedEntryPointSymbol
        := self error: "Not a function definition".
}.

Type definition: {
    public override method programEntityKindName => Symbol
        := #T.
}.

Metatype definition: {
    public override method printQualifiedNameOn: (stream: Stream) ::=> Void := {
        thisType ifNotNil: {
            thisType printQualifiedNameOn: stream.
            stream nextPutAll: ".__Meta__".
        } ifNil: {
            super printQualifiedNameOn: stream
        }
    }
}.

Namespace definition: {
    public override method programEntityKindName => Symbol
        := #N.
}.

Function definition: {
    public override method programEntityKindName => Symbol
        := #C.
}.

FunctionDefinition definition: {
    public override method programEntityKindName => Symbol
        := #F.

    public override method checkedEntryPointNameSymbol => FunctionCheckedEntryPointSymbol
        := FunctionCheckedEntryPointSymbol for: self.

    public override method uncheckedEntryPointNameSymbol => FunctionUncheckedEntryPointSymbol
        := FunctionUncheckedEntryPointSymbol for: self.

    public override method capturelessUncheckedEntryPointNameSymbol => Symbol
        := FunctionCapturelessUncheckedEntryPointSymbol for: self.
}.
