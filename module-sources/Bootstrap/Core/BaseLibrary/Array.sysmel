Array definition: {
    public override final eager method hash ::=> Size := {
        let result mutable := self class identityHash.
        self do: {:each :: Void |
            result := result hashConcatenate: each hash
        }.
        result
    }.

    public override final eager method = other ::=> Boolean := {
        (self class == other __type__) ifFalse: {return: false}.
        
        let otherArray => Array := other.
        self size = otherArray size ifFalse: {
            return: false
        }.

        let size := self size.
        let i mutable := 0sz.
        while: (i < size) do: {
            (self at: i) = (other at: i) ifFalse: {
                return: false
            }
        } continueWith: (i := i + 1sz).

        return: true
    }.

    public final method copyWithoutElementAt: (removedIndex: Size) ::=> self class := {
        let size := self size.
        removedIndex >= size ifTrue: {return: self}.

        let result := self class basicAllocate: size - 1sz.
        let destIndex mutable := 0sz.
        let i mutable := 0sz.
        while: (i < size) do: {
            i = removedIndex ifFalse: {
                result at: destIndex put: (self at: i).
                destIndex := destIndex + 1sz
            }.
        } continueWith:(i := i + 1sz).

        result 
    }.
}.