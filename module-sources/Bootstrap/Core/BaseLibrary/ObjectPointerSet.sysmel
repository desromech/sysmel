public final class ObjectPointerSet superclass: Set; definition: {
    public method untypedScanFor: (key: Untyped) ::=> IntPointer := {
        storage ifNil: { return: -1iptr }.

        let capacity := storage size.
        let naturalIndex := RawTuple::pointerIdentityHash(key) % capacity.

        let i mutable := naturalIndex.
        while: (i < capacity) do: {
            let element := storage untypedAt: i.
            if: (Untyped::==)(element, __hashtableEmptyElement__) || (Untyped::==)(key, element) then: {
                return: i asIntPointer
            }
        } continueWith: (i := i + 1sz).

        i := 0sz.
        while: (i < capacity) do: {
            let element := storage untypedAt: i.
            if: (Untyped::==)(element, __hashtableEmptyElement__) || (Untyped::==)(key, element) then: {
                return: i asIntPointer
            }
        } continueWith: (i := i + 1sz).

        -1iptr
    }.

    public virtual method scanFor: key ::=> IntPointer
        := self untypedScanFor: key.

    public method untypedAdd: (element: Untyped) ::=> Void := {
        let index mutable := self untypedScanFor: element.
        index < 0iptr ifTrue: {
            self increaseCapacity.
            index := self scanFor: element.
            self assert: index >= 0iptr
        }.
        
        let isNewElement := (Untyped::==)(storage untypedAt: index asSize , __hashtableEmptyElement__).
        storage at: index asSize put: element.
        isNewElement ifTrue: {
            size := size + 1sz.
            self checkCapacityIncreaseCondition
        }
    }.

    public method untypedIncludes: (element: Untyped) ::=> Boolean := {
        let index := self untypedScanFor: element.
        (index >= 0iptr) && Untyped::==(element, storage untypedAt: index asSize)
    }.

    public override final method add: element ::=> Void
        := self untypedAdd: element.

    public override pure final method includes: element ::=> Boolean
        := self untypedIncludes: element.
}.
