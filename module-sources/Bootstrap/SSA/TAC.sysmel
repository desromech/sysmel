public abstract class SSATACCodeGenerator superclass: Object; definition: {

}.

public final class SSAModuleTACCodeGenerator superclass: SSATACCodeGenerator; definition: {
    public field compilationTarget => TACCompilationTarget.
    public field targetModule => TACModule.
    public field globalValueDictionary => Dictionary.
    private field uncheckedFunctionEntryPointTrampoline => TACValue.
    private field checkedFunctionEntryPointTrampoline => TACValue.

    public method initializeWithTarget: (theCompilationTarget: TACCompilationTarget) ::=> Void := {
        compilationTarget := theCompilationTarget.
        targetModule := TACModule new
            compilationTarget: compilationTarget;
            yourself.
        globalValueDictionary := Dictionary new.
    }.

    public method compileSSAFunction: (function: SSAFunction) ::=> TACValue
        := globalValueDictionary at: function ifAbsent: {|
                ## TODO: Create a more adequate value for this.
                self compileSSAFunctionDefinition: function definition
            }.

    public method compileSSAFunctionDefinition: (functionDefinition: SSAFunctionDefinition) ::=> TACValue
        := globalValueDictionary at: functionDefinition ifAbsent: {|
                SSAFunctionDefinitionTACCodeGenerator new
                    moduleCodeGenerator: self;
                    compileSSAFunctionDefinition: functionDefinition
            }.

    public method finish => TACModule
        := targetModule.

    public method translateValue: (value: SSAValue) ::=> TACValue
        := globalValueDictionary at: value ifAbsent: {|
        value translateIntoTacValueWith: self
    }.

    public method validUncheckedFunctionEntryPointTrampoline => TACValue := {
        uncheckedFunctionEntryPointTrampoline ifNil: {
            uncheckedFunctionEntryPointTrampoline := targetModule build: #uncheckedFunctionEntryPointTrampoline function: {:(TACFunctionBuilder)functionBuilder :: Void |
            }
        }.

        uncheckedFunctionEntryPointTrampoline
    }.

    public method validCheckedFunctionEntryPointTrampoline => TACValue := {
        checkedFunctionEntryPointTrampoline ifNil: {
            checkedFunctionEntryPointTrampoline := targetModule build: #checkedFunctionEntryPointTrampoline function: {:(TACFunctionBuilder)functionBuilder :: Void |
            }
        }.

        checkedFunctionEntryPointTrampoline
    }.

    public method translateUncheckedFunctionEntryPoint: (value: SSAValue) ::=> TACValue := {
        self validUncheckedFunctionEntryPointTrampoline
    }.

    public method translateCheckedFunctionEntryPoint: (value: SSAValue) ::=> TACValue := {
        self validCheckedFunctionEntryPointTrampoline
    }.

    public method translateGenericObjectPointer: object ::=> TACValue
        := TACGenericObjectPointerConstant new
            size: compilationTarget pointerSize;
            alignment: compilationTarget pointerAlignment;
            object: object;
            type: compilationTarget uintPointerType;
            yourself.
}.

public final class SSAFunctionDefinitionTACCodeGenerator superclass: SSATACCodeGenerator; definition: {
    public field moduleCodeGenerator => SSAModuleTACCodeGenerator.
    public field ssaFunctionDefinition => SSAFunctionDefinition.
    public field tacFunction => TACFunction.
    public field functionBuilder => TACFunctionBuilder.
    public field blockBuilder => TACBlockBuilder.
    protected field valueDictionary => Dictionary.
    public field functionArgument => TACValue.
    
    public method initialize => Void := {
        valueDictionary := Dictionary new.
    }.

    public method compileSSAFunctionDefinition: (ssaFunctionDefinition: SSAFunctionDefinition) ::=> TACValue := {
        self ssaFunctionDefinition: ssaFunctionDefinition.
        moduleCodeGenerator targetModule build: ssaFunctionDefinition name function: {:(TACFunctionBuilder)functionBuilder :: Void |
            tacFunction := functionBuilder function.
            self functionBuilder: functionBuilder.
            moduleCodeGenerator globalValueDictionary at: ssaFunctionDefinition put: tacFunction.

            self compileDefinition.

            tacFunction
        }.
    }.

    public method compileDefinition => Void := {
        ## SSA must be destroyed before compiling.
        SSADestroySSAFunctionPass runIn: ssaFunctionDefinition.

        self
            declareArguments;
            declareCaptures;
            declareValues;
            compileBasicBlocks.
    }.

    public method declareArguments => Void := {
        let tacArguments := OrderedCollection new.
        functionArgument := tacFunction newArgument: #closure withType: Function.
        tacArguments add: functionArgument.
        
        ssaFunctionDefinition arguments do: {:(SSAFunctionArgument)argument :: Void |
            let tacArgument := tacFunction newArgument: argument name withType: argument type.
            valueDictionary at: argument put: tacArgument.
            tacArguments add: tacArgument.
        }.

        tacFunction arguments: tacArguments asArray.
    }.

    public method declareCaptures => Void := {
        let tacCaptures := OrderedCollection new.
        ssaFunctionDefinition captures do: {:(SSAFunctionCapture)capture :: Void |
            let tacCapture := tacFunction newCapture: capture name withType: capture type.
            valueDictionary at: capture put: tacCapture.
            tacCaptures add: tacCapture.
        }.

        tacFunction captures: tacCaptures asArray.
    }.

    public method declareValues => Void := {
        ssaFunctionDefinition basicBlocksDo: {:(SSABasicBlock)eachBlock :: Void |
            let tacBlock := TACBasicBlock new
                name: eachBlock name;
                yourself.

            tacFunction addBasicBlock: tacBlock.
            valueDictionary at: eachBlock put: tacBlock.

            eachBlock instructionsDo: {:(SSAInstruction)each :: Void |
                let tacValue := each makeTacValueForResultWith: self.
                valueDictionary at: each put: tacValue.
            }
        }
    }.

    public method compileBasicBlocks => Void := {
        ssaFunctionDefinition basicBlocksDo: {:(SSABasicBlock)eachBlock :: Void |
            let tacBlock := valueDictionary at: eachBlock.
            self compileBasicBlock: eachBlock into: tacBlock
        }.
    }.

    public method compileBasicBlock: (ssaBasicBlock: SSABasicBlock) into: (tacBlock: TACBasicBlock) ::=> Void := {
        blockBuilder := TACBlockBuilder new
            block: tacBlock;
            function: tacFunction;
            yourself.
        ssaBasicBlock instructionsDo: {:(SSAInstruction)eachInstruction :: Void |
            blockBuilder useDebugInfoOfSSAValue: eachInstruction.
            eachInstruction compileIntoTacValue: (valueDictionary at: eachInstruction) with: self
        }
    }.

    public method translateValue: (value: SSAValue) ::=> TACValue
        := valueDictionary at: value ifAbsent: {|
            moduleCodeGenerator translateValue: value
        }.
    
    public method translateBoxedValue: (value: SSAValue) ::=> TACValue
        := self translateValue: value.

    public method translateUncheckedFunctionEntryPoint: (value: SSAValue) ::=> TACValue
        := moduleCodeGenerator translateUncheckedFunctionEntryPoint: value.

    public method translateCheckedFunctionEntryPoint: (value: SSAValue) ::=> TACValue
        := moduleCodeGenerator translateCheckedFunctionEntryPoint: value.

}.

TACBlockBuilder extend: {
    public method useDebugInfoOfSSAValue: (value: SSAValue) ::=> Void := {
        debugSourcePosition := value sourcePosition.
        debugSourceNode := value sourceASTNode.
        debugSourceEnvironment := value sourceEnvironment.
    }
}.

Type extend: {
    public virtual method emitTacReturn: (value: TACValue) withBlockBuilder: (builder: TACBlockBuilder) ::=> Void := {
        builder returnPointer: value.
    }.

    public virtual method emitTacCall: (function: TACValue) convention: (callingConvention: TACCallingConvention) withArguments: (arguments: Array) into: (result: TACValue) withBlockBuilder: (builder: TACBlockBuilder) ::=> Void := {
        builder callPointer: function convention: callingConvention arguments: arguments result: result
    }.

    public virtual method makeTacTemporaryValueNamed: (name: Symbol) withFunctionBuilder: (functionBuilder: TACFunctionBuilder) ::=> TACValue
        := functionBuilder function newTemporary: name withType: self.

    public virtual method translateSSALiteralConstant: (constant: SSALiteralConstant) intoTacWith: (codegen: SSAModuleTACCodeGenerator) ::=> TACValue
        := codegen translateGenericObjectPointer: constant value.
}.

Char8 __type__ extend: {
    public override method emitTacReturn: (value: TACValue) withBlockBuilder: (builder: TACBlockBuilder) ::=> Void := {
        builder returnInt32: value
    }.

    public override method translateSSALiteralConstant: (constant: SSALiteralConstant) intoTacWith: (codegen: SSAModuleTACCodeGenerator) ::=> TACValue
        := constant value asUInt8 asTacConstant.
}.

Int8 __type__ extend: {
    public override method emitTacReturn: (value: TACValue) withBlockBuilder: (builder: TACBlockBuilder) ::=> Void := {
        builder returnInt32: value
    }.

    public override method translateSSALiteralConstant: (constant: SSALiteralConstant) intoTacWith: (codegen: SSAModuleTACCodeGenerator) ::=> TACValue
        := constant value asTacConstant.
}.

UInt8 __type__ extend: {
    public override method emitTacReturn: (value: TACValue) withBlockBuilder: (builder: TACBlockBuilder) ::=> Void := {
        builder returnInt32: value
    }.

    public override method translateSSALiteralConstant: (constant: SSALiteralConstant) intoTacWith: (codegen: SSAModuleTACCodeGenerator) ::=> TACValue
        := constant value asTacConstant.
}.

Char16 __type__ extend: {
    public override method emitTacReturn: (value: TACValue) withBlockBuilder: (builder: TACBlockBuilder) ::=> Void := {
        builder returnInt32: value
    }.

    public override method translateSSALiteralConstant: (constant: SSALiteralConstant) intoTacWith: (codegen: SSAModuleTACCodeGenerator) ::=> TACValue
        := constant value asUInt16 asTacConstant.
}.

Int16 __type__ extend: {
    public override method emitTacReturn: (value: TACValue) withBlockBuilder: (builder: TACBlockBuilder) ::=> Void := {
        builder returnInt32: value
    }.

    public override method translateSSALiteralConstant: (constant: SSALiteralConstant) intoTacWith: (codegen: SSAModuleTACCodeGenerator) ::=> TACValue
        := constant value asTacConstant.
}.

UInt16 __type__ extend: {
    public override method emitTacReturn: (value: TACValue) withBlockBuilder: (builder: TACBlockBuilder) ::=> Void := {
        builder returnInt32: value
    }.

    public override method translateSSALiteralConstant: (constant: SSALiteralConstant) intoTacWith: (codegen: SSAModuleTACCodeGenerator) ::=> TACValue
        := constant value asTacConstant.
}.

Char32 __type__ extend: {
    public override method emitTacReturn: (value: TACValue) withBlockBuilder: (builder: TACBlockBuilder) ::=> Void := {
        builder returnInt32: value
    }.

    public override method translateSSALiteralConstant: (constant: SSALiteralConstant) intoTacWith: (codegen: SSAModuleTACCodeGenerator) ::=> TACValue
        := constant value asUInt32 asTacConstant.
}.

Int32 __type__ extend: {
    public override method emitTacReturn: (value: TACValue) withBlockBuilder: (builder: TACBlockBuilder) ::=> Void := {
        builder returnInt32: value
    }.

    public override method translateSSALiteralConstant: (constant: SSALiteralConstant) intoTacWith: (codegen: SSAModuleTACCodeGenerator) ::=> TACValue
        := constant value asTacConstant.
}.

UInt32 __type__ extend: {
    public override method emitTacReturn: (value: TACValue) withBlockBuilder: (builder: TACBlockBuilder) ::=> Void := {
        builder returnInt32: value
    }.

    public override method translateSSALiteralConstant: (constant: SSALiteralConstant) intoTacWith: (codegen: SSAModuleTACCodeGenerator) ::=> TACValue
        := constant value asTacConstant.
}.

Int64 __type__ extend: {
    public override method emitTacReturn: (value: TACValue) withBlockBuilder: (builder: TACBlockBuilder) ::=> Void := {
        builder returnInt64: value
    }.

    public override method translateSSALiteralConstant: (constant: SSALiteralConstant) intoTacWith: (codegen: SSAModuleTACCodeGenerator) ::=> TACValue
        := constant value asTacConstant.
}.

UInt64 __type__ extend: {
    public override method emitTacReturn: (value: TACValue) withBlockBuilder: (builder: TACBlockBuilder) ::=> Void := {
        builder returnInt64: value
    }.

    public override method translateSSALiteralConstant: (constant: SSALiteralConstant) intoTacWith: (codegen: SSAModuleTACCodeGenerator) ::=> TACValue
        := constant value asTacConstant.
}.

Float32 __type__ extend: {
    public override method emitTacReturn: (value: TACValue) withBlockBuilder: (builder: TACBlockBuilder) ::=> Void := {
        builder returnFloat32: value
    }.

    public override method translateSSALiteralConstant: (constant: SSALiteralConstant) intoTacWith: (codegen: SSAModuleTACCodeGenerator) ::=> TACValue
        := constant value asTacConstant.
}.

Float64 __type__ extend: {
    public override method emitTacReturn: (value: TACValue) withBlockBuilder: (builder: TACBlockBuilder) ::=> Void := {
        builder returnFloat64: value
    }.

    public override method translateSSALiteralConstant: (constant: SSALiteralConstant) intoTacWith: (codegen: SSAModuleTACCodeGenerator) ::=> TACValue
        := constant value asTacConstant.
}.

Void __type__ extend: {
    public override method emitTacReturn: (value: TACValue) withBlockBuilder: (builder: TACBlockBuilder) ::=> Void := {
        builder returnVoid
    }.

    public override method makeTacTemporaryValueNamed: (name: Symbol) withFunctionBuilder: (functionBuilder: TACFunctionBuilder) ::=> TACValue
        := nil.
}.

SSAValue extend: {
    public abstract method translateIntoTacValueWith: (codegen: SSAModuleTACCodeGenerator) ::=> TACValue
        := self subclassResponsibility.
}.

SSALiteralConstant extend: {
    public override method translateIntoTacValueWith: (codegen: SSAModuleTACCodeGenerator) ::=> TACValue
        := type translateSSALiteralConstant: self intoTacWith: codegen.
}.

SSAInstruction extend: {
    public virtual method makeTacValueForResultWith: (codegen: SSAFunctionDefinitionTACCodeGenerator) ::=> TACValue
        := type makeTacTemporaryValueNamed: self name withFunctionBuilder: codegen functionBuilder.

    public abstract method compileIntoTacValue: (tacValue: TACValue) with: (codegen: SSAFunctionDefinitionTACCodeGenerator) ::=> Void
        := self subclassResponsibility.
}.

let translateUnaryPrimitiveInto := {:(TACOperation)operation :: ((SSACallInstruction, TACValue, SSAFunctionDefinitionTACCodeGenerator) => Boolean) |
    {:(SSACallInstruction)call :(TACValue)result :(SSAFunctionDefinitionTACCodeGenerator)codegen :: Boolean |
        let operand := codegen translateValue: call arguments first.
        codegen blockBuilder operation: operation with: operand.
        true
    }
}.

let translateBinaryPrimitiveInto := {:(TACOperation)operation :: ((SSACallInstruction, TACValue, SSAFunctionDefinitionTACCodeGenerator) => Boolean) |
    {:(SSACallInstruction)call :(TACValue)result :(SSAFunctionDefinitionTACCodeGenerator)codegen :: Boolean |
        let left := codegen translateValue: call arguments first.
        let right := codegen translateValue: call arguments second.
        codegen blockBuilder operation: operation with: left with: right result: result.
        true
    }
}.

public global SSATacPrimitiveCallGenerationTable := #{
    #UInt32::+ : translateBinaryPrimitiveInto(TAC::Operation::AddInt32).
    #UInt32::- : translateBinaryPrimitiveInto(TAC::Operation::SubInt32).
    #UInt32::* : translateBinaryPrimitiveInto(TAC::Operation::MulInt32).
    #UInt32::/ : translateBinaryPrimitiveInto(TAC::Operation::UDivInt32).
    #UInt32::% : translateBinaryPrimitiveInto(TAC::Operation::URemInt32).

    #UInt32::& : translateBinaryPrimitiveInto(TAC::Operation::BitAndInt32).
    #UInt32::| : translateBinaryPrimitiveInto(TAC::Operation::BitOrInt32).
    #UInt32::^ : translateBinaryPrimitiveInto(TAC::Operation::BitXorInt32).
    #UInt32::<< : translateBinaryPrimitiveInto(TAC::Operation::LogicalShiftLeftInt32).
    #UInt32::>> : translateBinaryPrimitiveInto(TAC::Operation::LogicalShiftRightInt32).

    #*
    #UInt32::= : translateBinaryPrimitiveInto(TAC::Operation::Int32Equals).
    #UInt32::~= : translateBinaryPrimitiveInto(TAC::Operation::Int32NotEquals).
    #UInt32::<=> : translateBinaryPrimitiveInto(TAC::Operation::UInt32Compare).
    #UInt32::< : translateBinaryPrimitiveInto(TAC::Operation::UInt32LessThan).
    #UInt32::<= : translateBinaryPrimitiveInto(TAC::Operation::UInt32LessOrEquals).
    #UInt32::> : translateBinaryPrimitiveInto(TAC::Operation::UInt32GreaterThan).
    #UInt32::>= : translateBinaryPrimitiveInto(TAC::Operation::UInt32GreaterOrEquals).
    *#

    #Int32::+ : translateBinaryPrimitiveInto(TAC::Operation::AddInt32).
    #Int32::- : translateBinaryPrimitiveInto(TAC::Operation::SubInt32).
    #Int32::* : translateBinaryPrimitiveInto(TAC::Operation::MulInt32).
    #Int32::/ : translateBinaryPrimitiveInto(TAC::Operation::SDivInt32).
    #Int32::% : translateBinaryPrimitiveInto(TAC::Operation::SRemInt32).

    #Int32::& : translateBinaryPrimitiveInto(TAC::Operation::BitAndInt32).
    #Int32::| : translateBinaryPrimitiveInto(TAC::Operation::BitOrInt32).
    #Int32::^ : translateBinaryPrimitiveInto(TAC::Operation::BitXorInt32).
    #Int32::<< : translateBinaryPrimitiveInto(TAC::Operation::LogicalShiftLeftInt32).
    #Int32::>> : translateBinaryPrimitiveInto(TAC::Operation::ArithmeticShiftRightInt32).
}.

SSACallInstruction extend: {
    public override method compileIntoTacValue: (tacValue: TACValue) with: (codegen: SSAFunctionDefinitionTACCodeGenerator) ::=> Void := {
        ## Attempt to use the primitive generator first.
        let primitiveGenerator := SSATacPrimitiveCallGenerationTable atOrNil: calledFunction primitiveName.
        primitiveGenerator ifNotNil: {
            primitiveGenerator(self, tacValue, codegen) ifTrue: {
                return: void
            }
        }.

        isUnchecked ifTrue: {
            let tacCalledFunction := codegen translateValue: calledFunction.
            let entryPoint := codegen translateUncheckedFunctionEntryPoint: calledFunction.
            let tacArguments := arguments collect: {:(SSAValue)eachArgument :: TACValue | codegen translateValue: eachArgument}.
            self type
                emitTacCall: entryPoint convention: codegen tacFunction compilationTarget defaultCallingConvention
                withArguments: (Array with: tacCalledFunction) -- tacArguments into: tacValue
                withBlockBuilder: codegen blockBuilder
        } ifFalse: {
            let tacCalledFunction := codegen translateBoxedValue: calledFunction.
            let entryPoint := codegen translateCheckedFunctionEntryPoint: calledFunction.
            let tacArguments := arguments collect: {:(SSAValue)eachArgument :: TACValue | codegen translateBoxedValue: eachArgument}.
            AnyValue
                emitTacCall: entryPoint convention: codegen tacFunction compilationTarget defaultCallingConvention
                withArguments: (Array with: tacCalledFunction) -- tacArguments into: tacValue
                withBlockBuilder: codegen blockBuilder
        }.
    }.
}.

SSAReturnInstruction extend: {
    public override method compileIntoTacValue: (tacValue: TACValue) with: (codegen: SSAFunctionDefinitionTACCodeGenerator) ::=> Void := {
        self value type emitTacReturn: (codegen translateValue: self value) withBlockBuilder: codegen blockBuilder
    }.
}.

SSAFunction extend: {
    public method asTACModuleFor: (target: TACCompilationTarget) ::=> TACModule
        := SSAModuleTACCodeGenerator new
            initializeWithTarget: target;
            compileSSAFunction: self;
            finish
}.
