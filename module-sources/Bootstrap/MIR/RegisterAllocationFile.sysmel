public final class MIRRegisterAllocationFile superclass: Object; definition: {
    public field usedRegisterSet => IdentitySet.
    public field availableRegisters => OrderedCollection.
    public field activeScratchRegisters => OrderedCollection.

    public field activeIntervals => MIRLiveIntervalSortedList.
    private field currentValueIndex => UInt32.

    __Meta__ definition: {
        public method for: (registers: Array) ::=> self
            := self new initializeWithRegisters: registers; yourself
    }.

    public method initializeWithRegisters: (allocatableRegisterList: Array) ::=> Void := {
        activeIntervals := MIRLiveIntervalSortedList new.
        availableRegisters := OrderedCollection new.
        allocatableRegisterList reverseDo: {:each :: Void | availableRegisters add: each}.
        usedRegisterSet := IdentitySet new.
        activeScratchRegisters := OrderedCollection new.
    }.

    public method usesRegister: (register: MIRPhysicalLocation) ::=> Boolean
        := usedRegisterSet includes: register.

    public method beginValueAtIndex: (valueIndex: UInt32) ::=> Void := {
        currentValueIndex := valueIndex.
        self expireOldLiveIntervals.
    }.

    public method expireOldLiveIntervals => Void := {
        availableRegisters addAll: activeScratchRegisters.
        activeScratchRegisters removeAll.

        activeIntervals expireUntil: currentValueIndex doing: {:(MIRLiveInterval)interval :: Void |
            interval addRegistersOnto: availableRegisters.
        }.
    }.

    public method freeAllocatedScratchRegisters => Void := {
        availableRegisters addAll: activeScratchRegisters.
        activeScratchRegisters removeAll.
    }.

    public method spillRegistersIfNeededExceptFor: (registerList: Array) ::=> Void := {
        activeIntervals
            expireAllThat: {:(MIRLiveInterval)interval :: Boolean | (interval includesRegisterInList: registerList) not }
            doing: {:(MIRLiveInterval)interval :: Void |
                interval addRegistersOnto: availableRegisters.
                interval spill.
            }
    }.

    public method spillGCRoots => Void := {
        activeIntervals spillGCRootsThatSurvive: currentValueIndex
    }.

    public method spillIfNeededAndAllocateRegister: (usedRegister: MIRPhysicalLocation) ::=> Void
        := self spillIfNeededAndAllocateRegister: usedRegister withValue: nil.

    public method addActiveRegister: (register: MIRPhysicalLocation) withValue: (value: MIRValue) ::=> Void := {
        usedRegisterSet add: register.
        value isNotNil && value isLocalValue ifTrue: {
            let localValue := (value uncheckedDownCastTo: MIRLocalValue) validCoalescedValue.
            localValue assignedRegister ifNil: {
                let interval := localValue liveInterval.
                interval isSpilled not && localValue hasPointerUsage not && localValue physicalLocationClass isRegister ifTrue: {
                    localValue assignedRegister: register.
                    interval registerOrSequence: register.
                    activeIntervals add: interval.
                    (activeScratchRegisters includes: register) ifTrue: {
                        activeScratchRegisters remove: register
                    }.
                    return: void.
                }
            }
        }.

        (activeScratchRegisters includes: register) ifFalse: {
            activeScratchRegisters add: register
        }.
    }.

    public method addActiveRegisterSequence: (registerSequence: MIRPhysicalLocationRegisterSequence) withValue: (value: MIRValue) ::=> Void := {
        registerSequence addRegistersOntoSet: usedRegisterSet.
        value isNotNil && value isLocalValue ifTrue: {
            let localValue := (value uncheckedDownCastTo: MIRLocalValue) validCoalescedValue.
            localValue assignedRegister ifNil: {
                let interval := localValue liveInterval.
                interval isSpilled not && localValue hasPointerUsage not && localValue physicalLocationClass isRegister ifTrue: {
                    localValue assignedRegister: registerSequence.
                    interval registerOrSequence: registerSequence.
                    activeIntervals add: interval.
                    registerSequence do: {:(MIRPhysicalLocationRegister)register :: Void |
                        (activeScratchRegisters includes: register) ifTrue: {
                            activeScratchRegisters remove: register
                        }.
                    }.
                    
                    return: void.
                }
            }
        }.

        registerSequence do: {:(MIRPhysicalLocationRegister)register :: Void |
            (activeScratchRegisters includes: register) ifFalse: {
                activeScratchRegisters add: register
            }.
        }
    }.

    public method spillIfNeededAndAllocateRegister: (usedRegister: MIRPhysicalLocation) withValue: (value: MIRValue) ::=> Void := {
        value isNotNil && (value coalescedAssignedRegister == usedRegister) ifTrue: {
            return: void
        }.

        activeIntervals
            expireAllThat: {:(MIRLiveInterval)interval :: Boolean | interval includesRegister: usedRegister }
            doing: {:(MIRLiveInterval)interval :: Void |
                interval addRegistersOnto: availableRegisters.
                interval spill.
            }.

        (availableRegisters includes: usedRegister) ifFalse: {
            self error: "Failed to allocate register " -- usedRegister printString.
        }.

        availableRegisters remove: usedRegister.
        self addActiveRegister: usedRegister withValue: value
    }.

    public method spillIfNeededAndAllocateRegister: (usedRegister: MIRPhysicalLocation) withValuePointer: (value: MIRValue) ::=> Void
        := self spillIfNeededAndAllocateRegister: usedRegister withValue: nil.

    public method spillIfNeededAndAllocateRegister: (usedRegister: MIRPhysicalLocation) withResultValue: (value: MIRValue) ::=> Void := {
        value isNotNil && (value coalescedAssignedRegister == usedRegister) ifTrue: {
            return: void
        }.

        ## Make sure we the required register is available or in the scratch set.
        (activeScratchRegisters includes: usedRegister) ifFalse: {
            activeIntervals
                expireAllThat: {:(MIRLiveInterval)interval :: Boolean | interval includesRegister: usedRegister }
                doing: {:(MIRLiveInterval)interval :: Void |
                    interval addRegistersOnto: availableRegisters.
                    interval spill.
                }.

            availableRegisters remove: usedRegister.
        }.

        self addActiveRegister: usedRegister withValue: value
    }.

    public method spillIfNeededAndAllocateRegisterSequence: (usedRegisterSequence: MIRPhysicalLocationRegisterSequence) withValue: (value: MIRValue) ::=> Void := {
        value isNotNil && (value coalescedAssignedRegister == usedRegisterSequence) ifTrue: {
            return: void
        }.

        activeIntervals
            expireAllThat: {:(MIRLiveInterval)interval :: Boolean | interval includesRegisterOfSequence: usedRegisterSequence }
            doing: {:(MIRLiveInterval)interval :: Void |
                interval addRegistersOnto: availableRegisters.
                interval spill.
            }.

        usedRegisterSequence do: {:(MIRPhysicalLocationRegister)usedRegister :: Void |
            (availableRegisters includes: usedRegister) ifFalse: {
                self error: "Failed to allocate register " -- usedRegister printString.
            }.

            availableRegisters remove: usedRegister.
        }.

        self addActiveRegisterSequence: usedRegisterSequence withValue: value
    }.

    public method spillIfNeededAndAllocateRegisterSequence: (usedRegisterSequence: MIRPhysicalLocationRegisterSequence) withResultValue: (value: MIRValue) ::=> Void := {
        value isNotNil && (value coalescedAssignedRegister == usedRegisterSequence) ifTrue: {
            return: void
        }.

        usedRegisterSequence do: {:(MIRPhysicalLocationRegister)usedRegister :: Void |
            (activeScratchRegisters includes: usedRegister) ifFalse: {
                activeIntervals
                    expireAllThat: {:(MIRLiveInterval)interval :: Boolean | interval includesRegister: usedRegister }
                    doing: {:(MIRLiveInterval)interval :: Void |
                        interval addRegistersOnto: availableRegisters.
                        interval spill.
                    }.

                availableRegisters remove: usedRegister.
            }.
        }.

        self addActiveRegisterSequence: usedRegisterSequence withValue: value
    }.

    public method ensureAvailableRegisters: (count: Size) ::=> Void := {
        while: (availableRegisters size < count) do: {
            let interval := activeIntervals removeLast.
            interval addRegistersOnto: availableRegisters.
            interval spill.
        }.
    }.

    public method ensureAvailableRegister => Void
        := self ensureAvailableRegisters: 1sz.

    public method ensureAvailableByteRegister => Void := {
        (availableRegisters anySatisfy: {:(MIRPhysicalLocation)each :: Boolean | each canBeUsedForByte }) ifFalse: {
            (activeIntervals
                expireAnyThat: {:(MIRLiveInterval)interval :: Boolean | interval registerOrSequence canBeUsedForByte }
                doing: {:(MIRLiveInterval)interval :: Void |
                    interval registerOrSequence ifNotNil: {
                        interval addRegistersOnto: availableRegisters.
                        interval spill.
                    }
                }) ifFalse: {
                    self error: "Failed to spill byte register"
                }
        }.
    }.

    public method allocateForValue: (value: MIRValue) ::=> MIRPhysicalLocation := {
        let existentAsignment := value ifNotNil: value coalescedAssignedRegister.
        existentAsignment ifNil: {
            self ensureAvailableRegister.
            let register => MIRPhysicalLocation := availableRegisters removeLast.
            self addActiveRegister: register withValue: value.
            register
        }.
    }.
    
    public method allocatePairForValue: (value: MIRValue) ::=> MIRPhysicalLocation := {
        let existentAsignment := value ifNotNil: value coalescedAssignedRegister.
        existentAsignment ifNil: {
            self ensureAvailableRegisters: 2sz.
            let second => MIRPhysicalLocationRegister := availableRegisters removeLast.
            let first => MIRPhysicalLocationRegister := availableRegisters removeLast.
            let sequence := MIRPhysicalLocationRegisterPair with: first with: second.
            self addActiveRegisterSequence: sequence withValue: value.
            sequence
        }.
    }.

    public method allocateForValuePointer: (value: MIRValue) ::=> MIRPhysicalLocation
        := self allocateForValue: nil.

    public method allocate => MIRPhysicalLocation
        := self allocateForValue: nil.

    private method attemptToAllocateSameResultFor: (value: MIRValue) sharingWith: (sharingValue: MIRValue) ::=> Void := {
        value isNotNil && value isLocalValue && sharingValue isNotNil && sharingValue isLocalValue ifTrue: {
            let localValue := value downCastTo: MIRLocalValue.
            let sharingLocalValue := sharingValue downCastTo: MIRLocalValue.
            let sharingInterval := sharingLocalValue liveInterval.

            (localValue physicalLocationClass == sharingLocalValue physicalLocationClass) && 
            sharingInterval isSpilled not &&
            sharingInterval registerOrSequence isNotNil && 
            (sharingInterval end <= currentValueIndex) ifTrue: {
                activeIntervals
                    expireAllThat: {:(MIRLiveInterval)interval :: Boolean | interval == sharingInterval }
                    doing: {:(MIRLiveInterval)interval :: Void |
                        interval addRegistersOnto: availableRegisters.
                    }
            }
        }
    }.

    public method allocateForResultValue: (value: MIRValue) trySharingWith: (sharingValue: MIRValue) ::=> MIRPhysicalLocation := {
        self attemptToAllocateSameResultFor: value sharingWith: sharingValue.
        self allocateForValue: value.
    }.

    public method allocatePairForResultValue: (value: MIRValue) trySharingWith: (sharingValue: MIRValue) ::=> MIRPhysicalLocation := {
        self attemptToAllocateSameResultFor: value sharingWith: sharingValue.
        self allocatePairForValue: value.
    }.

    public method allocateForResultValuePointer: (value: MIRValue) ::=> MIRPhysicalLocation
        := self allocateForValue: nil.

    public method allocateForByteValue: (value: MIRValue) ::=> MIRPhysicalLocation := {
        let existentAsignment := value ifNotNil: value coalescedAssignedRegister.
        existentAsignment ifNotNil: { return: existentAsignment }.

        self ensureAvailableByteRegister.
        
        let availableCount := availableRegisters size.
        let i mutable := availableCount.
        while: (i > 0sz) do: {
            i := i - 1sz.
            let register => MIRPhysicalLocation := availableRegisters at: i.
            register canBeUsedForByte ifTrue: {
                availableRegisters removeAt: i.
                self addActiveRegister: register withValue: value.
                return: register
            }.
        }.

        self error: "Failed to allocate byte register.".
    }.

    public method allocateForByte => MIRPhysicalLocation
        := self allocateForByteValue: nil.

    public method allocateForByteResultValue: (value: MIRValue) trySharingWith: (sharingValue: MIRValue) ::=> MIRPhysicalLocation
        := self allocateForByteValue: value.
}.
