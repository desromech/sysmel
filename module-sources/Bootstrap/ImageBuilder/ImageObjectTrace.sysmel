public class ImageObjectTrace superclass: Object; definition: {
    public field objectList => OrderedCollection.
    public field objectIndexDictionary => IdentityDictionary.
    public field patchDictionary => IdentityDictionary.

    public override method initialize => Void := {
        super initialize.

        objectList := OrderedCollection new.
        objectIndexDictionary := IdentityDictionary new.
        patchDictionary := IdentityDictionary new.
    }.

    public method addObject: (object: Untyped) patch: (patch: ImageObjectTracePatch) ::=> Void := {
        patchDictionary untypedAt: object put: patch
    }.

    public method includesObject: (object: Untyped) ::=> Boolean
        := objectIndexDictionary untypedIncludesKey: object.

    public method addNewObject: (object: Untyped) ::=> UInt32 := {
        let index := objectList size asUInt32.
        objectList untypedAdd: object.
        objectIndexDictionary untypedAt: object put: index.
        index
    }.

    public method addObject: (object: Untyped) ::=> UInt32 := {
        objectIndexDictionary untypedAt: object ifAbsentPut: { :: UInt32 |
            let index := objectList size asUInt32.
            objectList add: object.
            index
        }.
    }.

    public method indexOfObject: (object: Untyped) ifPresent: presentBlock ifAbsent: absentBlock := {
        (objectIndexDictionary untypedIncludesKey: object) ifTrue: {
            presentBlock(objectIndexDictionary untypedAt: object)
        } ifFalse: {
            absentBlock()
        }
    }.
}.
