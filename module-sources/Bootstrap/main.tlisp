## Type layout descriptions. This must match the definition in the tuuvm/type.h
(define Type::nameSlotIndex 0)
(define Type::ownerSlotIndex 1)
(define Type::supertypeSlotIndex 2)
(define Type::slotsSlotIndex 3)
(define Type::totalSlotCountSlotIndex 4)
(define Type::flagsSlotIndex 5)
(define Type::macroMethodDictionarySlotIndex 6)
(define Type::methodDictionarySlotIndex 7)
(define Type::fallbackMethodDictionarySlotIndex 8)

(define TypeSlot::nameSlotIndex 0)
(define TypeSlot::flagsSlotIndex 1)
(define TypeSlot::typeSlotIndex 2)

(define Function::flagsSlotIndex 0)
(define Function::getterFlags (Size::fromInteger 24)) ## Pure and Final
(define Function::setterFlags (Size::fromInteger 16)) ## Final

## Some utility functions.

## until:do:
(define (until:do: stop block)
    (define ibox (RawTuple::new 1))
    (define (i) (RawTuple::slotAt: ibox 0))
    (define (i: v) (RawTuple::slotAt:put: ibox 0 v))
    (i: 0)
    (while:do:continueWith: (Integer::< (i) stop)
        (block (i))
        (i: (Integer::+ (i) 1)))
)

## exportInstrinsicBuiltInBinding
(define (exportInstrinsicBuiltInBinding symbol binding)
    (Environment::setSymbol:bindingWithValue: BootstrapEnv::IntrinsicsBuiltInEnvironment symbol binding))

## Type::validMethodDictionary
(define (Type::validMethodDictionary type)
    (define existingMethodDictionary (RawTuple::slotAt: type Type::methodDictionarySlotIndex))
    (if:then:else: (== existingMethodDictionary nil)
        (begin
            (define newMethodDictionary (MethodDictionary::new))
            (RawTuple::slotAt:put: type Type::methodDictionarySlotIndex newMethodDictionary)
            newMethodDictionary)
        existingMethodDictionary))

## Setup a getter method.
(define (setupGetter getter)
    (RawTuple::slotAt:put: getter Function::flagsSlotIndex Function::getterFlags)
    (Function::recompileAndOptimize getter))

## Setup a setter method.
(define (setupSetter setter)
    (RawTuple::slotAt:put: setter Function::flagsSlotIndex Function::setterFlags)
    (Function::recompileAndOptimize setter))

## Bootstrap environment reflection core.
(define (makeTypeAccessors type)
    (define typeName (RawTuple::slotAt: type Type::nameSlotIndex))

    (define (exportAndBindInstanceMethod selector method)
        (define exportedName (StringSymbol::intern (String::concat: typeName (String::concat: "::" (asString selector)))))
        (MethodDictionary::at:put: (Type::validMethodDictionary type) selector method)
        (exportInstrinsicBuiltInBinding exportedName method))

    (define slots (RawTuple::slotAt: type Type::slotsSlotIndex))
    (define slotCount (RawTuple::size slots))
    (define supertype (RawTuple::slotAt: type Type::supertypeSlotIndex))
    (define supertypeSlotCount (if:then:else: (== supertype nil)
        0
        (RawTuple::slotAt: supertype Type::totalSlotCountSlotIndex)))
    (until:do: slotCount (lambda (localSlotIndex)
        (define slotIndex (Size::+ supertypeSlotCount localSlotIndex))
        (define slot (RawTuple::slotAt: slots localSlotIndex))
        (define slotName (RawTuple::slotAt: slot TypeSlot::nameSlotIndex))
        (define slotFlags (RawTuple::slotAt: slot TypeSlot::flagsSlotIndex))
        (define slotType (RawTuple::slotAt: slot TypeSlot::typeSlotIndex))

        ## Getter
        (exportAndBindInstanceMethod slotName
            (setupGetter (lambda (self)
                (RawTuple::slotAt: self slotIndex))))

        ## Setter
        (exportAndBindInstanceMethod (StringSymbol::intern (String::concat: (asString slotName) ":"))
            (setupSetter (lambda (self value)
                (RawTuple::slotAt:put: self slotIndex value)))
        ))))

(define intrinsicTypeCount (RawTuple::slotAt: BootstrapEnv::IntrinsicTypes 0))
(define intrinsicTypes (RawTuple::slotAt: BootstrapEnv::IntrinsicTypes 1))
(until:do: intrinsicTypeCount (lambda (i)
    (define intrinsicType (RawTuple::slotAt: intrinsicTypes i))
    (makeTypeAccessors intrinsicType)
    ))

## Type::withSelector:addMethod:
(define (Type::withSelector:addMethod: type selector method)
    (define methodDict (Type::validMethodDictionary type))
    (define existentMethod (MethodDictionary::atOrNil: methodDict selector))
    (define isCorePrimitive (Function::isCorePrimitive existentMethod))
    (if:then:else: isCorePrimitive
        (Function::adoptDefinitionOf: existentMethod method)
        (MethodDictionary::at:put: methodDict selector method))
    (Type::flushLookupSelector: type selector))

## Type::validMacroMethodDictionary
(define (Type::validMacroMethodDictionary type)
    (define existingMethodDictionary (Type::macroMethodDictionary type))
    (if:then:else: (== existingMethodDictionary nil)
        (begin
            (define newMethodDictionary (MethodDictionary::new))
            (Type::macroMethodDictionary: type newMethodDictionary)
            newMethodDictionary)
        existingMethodDictionary))

## Type::withSelector:addMacroMethod:
(define (Type::withSelector:addMacroMethod: type selector method)
    (define methodDict (Type::validMacroMethodDictionary type))
    (MethodDictionary::at:put: methodDict selector method)
    (Type::flushMacroLookupSelector: type selector))

(Type::withSelector:addMethod: SourcePosition #printString (lambda (sourcePosition)
    (define sourceDirectory (SourceCode::directory (SourcePosition::sourceCode sourcePosition)))
    (define sourceName (SourceCode::name (SourcePosition::sourceCode sourcePosition)))
    (define sourcePath (FileSystem::joinPath: sourceDirectory sourceName))

    (define startLine (SourcePosition::startLine sourcePosition))
    (define startColumn (SourcePosition::startColumn sourcePosition))
    (define startString (String::concat: (String::concat: (asString startLine) ".") (asString startColumn)))

    (define endLine (SourcePosition::endLine sourcePosition))
    (define endColumn (SourcePosition::endColumn sourcePosition))
    (define endString (String::concat: (String::concat: (asString endLine) ".") (asString endColumn)))

    (String::concat: (String::concat: (String::concat: sourcePath ":") startString)
        (String::concat: "-" endString))
    ))

(exportInstrinsicBuiltInBinding #Type::withSelector:addMacroMethod: Type::withSelector:addMacroMethod:)
(exportInstrinsicBuiltInBinding #Type::validMacroMethodDictionary Type::validMacroMethodDictionary)

(exportInstrinsicBuiltInBinding #Type::withSelector:addMethod: Type::withSelector:addMethod:)
(exportInstrinsicBuiltInBinding #Type::validMethodDictionary Type::validMethodDictionary)

## Register the Type methods.
(Type::withSelector:addMethod: Type #withSelector:addMethod: Type::withSelector:addMethod:)
(Type::withSelector:addMethod: Type #validMethodDictionary Type::validMethodDictionary)

(Type::withSelector:addMethod: Type #withSelector:addMacroMethod: Type::withSelector:addMacroMethod:)
(Type::withSelector:addMethod: Type #validMacroMethodDictionary Type::validMacroMethodDictionary)

## Load the sysmel sources.
(loadSourceNamed: "Bootstrap.sysmel")
